{alpha:alt} ' c64list3.50: converts to upper/lowercase quoted strings
{number:0}
	on peek(829) goto {:3000},{:4000}
{number:1000}
	goto {:1811}
{:1001}
	gosub {:1002}:&,22,1
{:1002}
	print# 15,"p{$02}"chr$(x and 255)chr$(x/256)"{$01}":return
	&,53,a:im=2+a:goto {:2426}
{:1004}
	a=val(mid$(fl$,a,1)):return
{:1005}
	pl=.:goto {:1028}
{:1006}
	an$=uu$:on-(an$<>"") gosub {:1260}:on -(an$<>"") goto {:1264}:pl=1:&,1:goto {:1260}
{:1007}
	&"{pound}g1":a$=an$:return
{:1008}
	&"{f6}Device,Drive: {pound}i1":an$="  "+an$:return
{:1009}
	dv%(.)=d1%:dr%(.)=d2%:dv%=dv%(dr):dr$=mid$(str$(dr%(dr)),2)+":":d3%=dr%(dr):return
{:1010}
	close 15:gosub {:1009}:open 15,dv%,15:return
{:1011}
	close 2:gosub {:1010}:open 2,dv%,2,dr$+a$
{:1012}
	input# 15,e%,e$,t%,s%:a$="{f6}Status:{pound}#2{pound}%e:{pound}$e:{pound}%t:{pound}%s{f6}":return
{:1013}
	dr=5
{:1014}
	cm$=a$+"":gosub {:1360}:gosub {:1010}:if a$=pr$then e%=.:return
	a$=dr$+"+."+a$:load a$,dv%:pr$=cm$:lm$="":goto {:1012}
{:1016}
	gosub {:1013}:goto {:3000}
{:1019}
{number:1019}
	a=int(mn%/60):pt%=-(a>=p2% and a<p3%)*sgn(p1%):return
{:1023}
{number:1023}
	gosub {:1024}:a$=a$+",s,w":goto {:1011}
{:1024}
	gosub {:1010}:print# 15,"s"dr$a$:return
{:1025}
' x-tec removed "gosub 1001" here - lets SB/WF/VF save now
' 1/17/2018: remove "input# 2,st(x):" -  breaks +/NM.maint
	st(x)=st(x)+i
{:1026}
	gosub {:1001}:print# 2,st(x):return
{:1027}
	dr=3:a$="e."+b$+",s,"+a$:goto {:1011}
{:1028}
	poke 53249,pl:lp=.:&,1:return
	&:return
{:1030}
	sh=.:rc=.:poke 2038,.:poke 2024,-(qe=.and rq=1)*255
	&,3,sr:s=st
	poke 144,s and 255:a=rc:gosub {:1914}:rc=a:return
{:1034}
{number:1034}
	im=7:goto {:2400}
	im=1:goto {:2403}
	im=2:goto {:2403}
{:1038}
{number:1038}
' add user to u.index:
	im=8:goto {:2400}
' 1039: QM - QuickMail
	im=1:goto {:2401}
' 1040
	im=9:goto {:2400}
' 1041: TT - top ten list
	im=2:goto {:2401}
' 1042: PS - personal signatures
	im=3:goto {:2401}
{:1045}
{number:1045}
' 1045: load "++ index", open cmd channel, get highest user # in uh% and a%
	im=11:goto {:2400}
' 1046
	im=10:goto {:2400}
' 1047: ME - Exchange ID with another user (sysop only)
	im=13:goto {:2400}
' 1048
	pr$="":im$="":lm$="":return
' 1049: ST - User statistics
	im=1:goto {:2412}
' 1050: BA - Board Activity Register
	im=1:goto {:2414}
{:1060}
{number:1060}
	dr=3:a$="e.stats":goto {:1011}
{:1061}
	dr=3:a$="e.access":goto {:1011}
{:1062}
	dr=2:a$="m."+left$(tt$,14)+",s,"+a$:goto {:1011}
{:1063}
	dr=3:a$="e.data":goto {:1011}
{:1064}
	b$="log"+left$(lt$,am):goto {:1027}
{:1065}
	dr=6:a$="u.config":goto {:1011}
{number:1067}
	cm$=a$:gosub {:1360}:gosub {:1014}:f1=sgn(e%):goto {:3000}
' 1068: append or write e.b$:
	a$="a":gosub {:1027}:if e%then a$="w":gosub {:1027}
' 1069: write user account data:
	print# 2,na$r$id;r$d1$r$ac%r$ph$r$rn$r$co$r$ad$:return
	im=1:goto {:2400}
	im=2:gosub {:2400}
{:1072}
{number:1072}
	&,52,17,lf:&,52,20,em:poke 951,ll%:poke 971,mp%:poke 970,.:return
	im=1:goto {:2426}
{:1074}
{number:1074}
' write tt$() array elements 1 - (kk-1) to open lfn#2:
	&,14,2,kk-1:print# 2,"{up arrow}":close 2:goto {:1081}
{number:1075}
{:1075}
	&"{clear}"
{number:1076}
{:1076}
	gosub {:1010}:close 5:open 5,dv%,5,dr$+a$+",s,r":c$=b$:gosub {:1012}:b$=c$:&"{f6}"
	if e%=.then sr=5:gosub {:1030}:sr=2
	close 5:return
{:1079}
{number:1079}
	if dr<7 then if bf(dr)>-1 then gosub {:1009}:a=bf(dr):bf=a:return
{:1081}
{number:1081}
	gosub {:1010}:on-(dv%<>lk%)goto {:1083}:a$=dr$:if dr$="10:"then a$="a"
	a=2:print# 15,"lg"left$(a$,1):input# 15,e%,a$,a$,a$,a$,a$:goto {:1084}
{:1083}
{number:1083}
	a=1:close 2:open 2,dv%,.,"$"+dr$+"{black}":get#2,a$,a$:&,8,2,1:&,8,2,1:close 2
{:1084}
{number:1084}
	a=val(a$)*a:bf=a:goto {:1400}
	a$="{black},u,w":gosub {:1011}:close 2
	a$="{black}":gosub {:1024}:a=-sgn(e%):return
{number:1088} ' BF - blocks free
	im=5:goto {:2404}
' 1089: DR - disk directory w/X-Tec's auto-wildcard mod
	gosub {:1470}:on a goto {:1995}:&"{f6}Pattern: ":gosub {:1499}:b$="*"
	for i=1 to len(an$):if mid$(an$,i,1)="*" then b$=""
	next:close 1:open 1,dv%,.,"$"+dr$+an$+b$:&"{f6}":&,8,1:close 1:return
{:1093}
{number:1093}
	for i=1 to 4:&,11,i:next
	&,11,.:return
' 1095: log a$ to disk unless using immediate login:
	on i%goto {:1603}:gosub {:1490}
{:1096}
{number:1096}
' is "print log entries" enabled?
	a=a%:&,52,13,3:b=a%:a%=a:on b+1 goto {:1603}:open 16,4,15:close 16:if st then return
	print# 4,a$:return
{:1098}
{number:1098}
	cm$=p$:gosub {:1360}:gosub {:1100}
' TODO: increase cm$ string length (21)
	cm$=left$(p$,11)+":"+left$(an$,4):gosub {:1360}:cm$=p$:return
{:1100}
{number:1100}
	s=.:sh=.:gosub {:1914}:poke 53252,38:gosub {:1908}:&"{f6:2}":goto {:1006}
{:1196}
{number:1196}
	if dv%=lk%then print# 15,"l800"
	return
' 1198: TZ - change user's time zone
	im=4:goto {:2428}
{:1200}
{number:1200}
	dr$=mid$(str$(dr%(5)),2)+":":dv%=dv%(5):pr$="":load dr$+"image",dv%
{:1260}
{number:1260}
' ^?, activity queue
	on-(an$="{f8}{f2}")goto {:1265}
' ^x, recall command #x
	if left$(an$,1)="{f8}"then an$=hs$(10-val(mid$(an$,2,1))):goto {:1264}
{:1262}
{number:1262}
' 140=Image ^
	&,15,6,140:uu$=an$:an$=a$
{:1263}
{number:1263}
	for ct=1 to 9:hs$(ct)=hs$(ct+1):next:hs$(10)=an$:return
{:1264}
{number:1264}
	&p$+": {pound}v7{f6}":return
{:1265}
{number:1265}
	for ct=1 to 10:a%=10-ct:a$=hs$(ct):&"{f8}{pound}%a: {pound}$a{f6}":next:an$="":uu$="":return
{:1300}
{number:1300}
	a$=z$:y$=pr$:gosub {:1016}:a$=y$:on-(lc<>1)goto {:1013}:return
{:1301}
{number:1301}
	a$=z$:goto {:1016}
{number:1305}
	im=3:goto {:2404}
{number:1318}
' EM - email
	br=.:mm=.:a=9:gosub {:1004}:on-(a<>.)goto {:1301}:x=4:gosub {:1980}:goto {:1812}
{:1320}
{number:1320}
' &,52,14,3: is "Disable U/D Section" enabled?
	&,52,14,3:if a%then x=1:gosub {:1980}:goto {:1812}
	gosub {:1019}:a=5:gosub {:1004}:b=a:a=15:gosub {:1004}
' &,52,15,3: is "300 Baud U/D Lockout" enabled?
	&,52,15,3:if b=. or (pt% and a=.) or (a%<>. and qb<1200) then x=4:gosub {:1980}:goto {:1812}
{:1323}
{number:1323}
	f1=.:a$=z$+str$(nl):gosub {:1350}:a$=z$:goto {:1301}
{number:1330} ' ZZ - local mode
	im=7:goto {:2404}
{number:1332}
	im=8:goto {:2404}
{number:1335} ' VERSION - BBS version, ML date/revision
	&,5:a=lp:lp=1:&"{f6}Image BBS 64 V{pound}!a.{pound}%a: {pound}$a{f6}"
{:1336}
' IM date also displayed at boot time
	an$="51808308224":&,15:&"IM Revision: "+left$(an$,26)+"M{f6}":return
{number:1340}
' SB, UD, UX
	bn=.:r=int(val(mid$(an$,3))):if r<1 then r=1
{number:1342}
	on-(z$="{$d5}{$c4}"or z$="{$d5}{$d8}")goto {:1320}:goto {:1323}
{:1345}
{number:1345}
	gosub {:1351}:goto {:1812}
{number:1348} ' NU - New User rules/etc.
	a$="new user":goto {:1350}
' 1349: CF - BBS configuration
	a$="config"
{:1350}
{number:1350}
	a$="s."+a$:dr=1:goto {:1075}
{:1351}
{number:1351}
	im=1:goto {:2430}
' 1352: ?? - local mode commands menu
	im=2:goto {:2404}
' 1353: read text editor menu
' TODO: integrate this
	dr=1:a$="s.menu 3":goto {:1075}
'	a$="menu 3":goto {:1350}

' 1354: RD - read a SEQ file
	im=4:goto {:2404}
{:1360}
{number:1360}
' peek(17139)=0: screen mask on
' &,69,<col>,<row>,"string",$color ($8x=reverse; 0-based)
' fixed: strings can be up to 21 chars now (I=000 is idle timer now)
' fixed: clear cm$ area
' fixed: lines are short enough to edit on c64
' maybe: specify color of title as $80+c, for reverse text)
' to clear Area: cm$="":gosub 1360
	if peek(17139)=. then:&,69,4,21," "+left$(cm$+"{space:21}",21)+" ",$8c
	return
{:1370}
{number:1370}
	&,69,c,22,right$(str$(a+1e5),b),$81:return
{number:1372}
	&,40
' 1373: check free memory:
	z%=(peek(52)-peek(50))*256+peek(51)-peek(49):return
' 1374: screen mask, line 22: display c=00000 (total calls to system) and n=000 (calls today?)
	a=ca:b=5:c=2:gosub {:1370}:a=cn:b=3:c=10:goto {:1370}
{number:1375}
	a$=co$(co%):co$=a$
{:1376}
{number:1376}
' display a$ in 16 character message window
	an$=a$:&,9:return
' clear message window
	a$="{space:16}":goto {:1376}
{:1400}
{number:1400}
	for a=1 to 6:if dv%(a)=dv%and dr%(a)=val(dr$)then bf(a)=bf
{number:1402}
	next:a=bf:return
{number:1450} ' DC - DOS command
	gosub {:1470}:on a goto {:1995}:&"{f6}Disk Command?: ":poke 53252,58
{number:1452}
	gosub {:1499}:z$=an$:if z$=""or z$="@"then gosub {:1010}:gosub {:1012}:&:return
	b$=left$(z$,1):if b$="n"or b$="s"then gosub {:1901}:if a=.then return
	gosub {:1010}:print# 15,z$:gosub {:1012}:&:return
{number:1460} ' CA - Credit Add (sysop only)
	im=1:goto {:2404}
{:1470}
{number:1470}
' 1470: evaluate DCdv,dr command line
	a=int(val(mid$(an$,3,2))):if a>.then if a<7 then dr=a:goto {:1474}
	if a<7 or a>29 then a=8
{:1472}
{number:1472}
' 133=Image ","
	&,15,6,133:d2%=val(an$):d1%=a:dr=.:dv%(.)=d1%:dr%(.)=d2%
{:1474}
{number:1474}
	gosub {:1010}:close 15:a=-(st<>.):return
{:1490}
{number:1490}
	c$=a$:a$="a":gosub {:1064}:if e%=.then print# 2,c$
{number:1492}
	close 2:a$=c$:an$=c$:return
{:1499}
{number:1499}
	&"{pound}i0":if an$=""then return
' 1500: un-translate "special" f-key characters in an$
{:1500}
{number:1500}
	&,15,2:return
{:1520}
{number:1520}
	a=mn%:return
{:1530}
{number:1530}
' beep x times:
	&,25,.,x
{:1603}
{number:1603}
	return
{number:1604}
' load im.editor, start with empty buffer
	im=1:goto {:2402}
{number:1610}
' load im.editor, preserve existing buffer
	im=2:goto {:2402}
{:1630}
{number:1630}
	f1=.:gosub {:1920}:goto {:1812}
{:1634}
{number:1634}
	gosub {:1061}:x=ac%+1:gosub {:1001}:&,2,2:ag$=a$:&,2,2:close 2
	if len(fl$)<len(a$)then fl$=fl$+mid$(a$,len(fl$)+1)
	if len(fl$)>len(a$)then fl$=a$
	if ac%<>ao%then fl$=a$:ao%=ac%
	a=6:gosub {:1004}:le=(a+1)*10:a=16:gosub {:1004}:poke 830,a:a=20:gosub {:1004}:da%=a
	return
' 1640: C - chat request
	im=4:goto {:2400}
{number:1642} ' LD - last date
	im=1:goto {:2408}
{number:1644} ' LG - BBS activity log
	im=2:goto {:2408}
{number:1646} ' EX? - exchange credits for time
' load im.comm1
' TODO: +.lo: at login, if caller is on last call for today, but has credits, can buy another call
	im=3:goto {:2408}
{number:1648}
	im=4:goto {:2408}
{number:1650} ' TM - Mini Term
' FIXME: BBS reports "unknown command", even though local mode enabled
' and ECS command active
	im=1:goto {:2410}
{number:1652}
	im=2:goto {:2410}
{number:1654}
	im=3:goto {:2410}
{number:1656} ' T - time/date
	im=5:goto {:2400}
{number:1660}
' EP - Edit Parameters
	&"{f6}Edit (I)nfo or (P)arameters? {pound}g1{pound}v7{f6}":a=-(an$="P")-2*(an$="I")
{number:1661}
	im=1:on a goto {:2406},{:2428}:return
{number:1662}
' AT - change graphics translation
	im=2:goto {:2406}
{number:1664}
' PW - change password
	im=2:goto {:2428}
{number:1666}
' PR - change transfer protocol
	im=4:goto {:2406}
{number:1667}
	im=5:goto {:2406}
{number:1668}
' RL - clears strings?
	im$="":pr$="":lm$="":return
{number:1670}
' RP - read program
	im=6:goto {:2404}
{number:1678}
' F - feedback
	b=.:o$="Feedback":&"{f6}Leave {pound}$o?: ":gosub {:1902}:if a=. then return
{number:1680}
' check blocks free; <35?
	dr=3:gosub {:1079}:if a<35 then {:1978}
{number:1681}
	im=1:goto {:2422}
' Pinacolada: subroutined this, moved from 1702 -> 1686
{number:1686}
{:sub.logoff_option}
' 1907 Yes/No defaults to Yes
	if right$(an$,1)<>"!" then:&"{f6:2}Logoff?: ":gosub {:1907}
{number:1688}
	return

{:1690}
{number:1690}
' logoff options
' 12/31/2017: move "if...then...gosub" (43 bytes) to {:sub.logoff_option} (1702)
'	f4=.:if right$(an$,1)<>"!"then:&"{f6:2}Logoff?: ":gosub {:1907}:if a=.then return
	f4=.:gosub {:sub.logoff_option}:if a=. then return
{number:1692}
	f4=1.3:return
{:1694}
{number:1694}
'	a=1:if right$(an$,1)<>"!"then:&"{f6:2}Logoff?: ":gosub {:1907}
	gosub {:sub.logoff_option}:if a=. then f1=-3*(x=2):on x goto {:1630},{:3000},{:3000}
{:1696}
{number:1696}
	cd%=.:if mid$(z$,2,1)<>"%"then ld$=lt$
	if right$(an$,1)="!"then f1=2:goto {:1700}
{:1698}
{number:1698}
	f1=1:if tr%<1 then b$="Time":gosub {:1985}:f1=2
{:1700}
{number:1700}
	a$="lo":goto {:1016}
{number:1736}
{:1736}
' skip loading already loaded protocol
	if a=pr then {:1740}
{number:1738}
{:1738}
	pr=a:dr=5:gosub {:1009}
	a$=dr$+"++"+str$(a):&,7,dv%,2
{number:1740}
{:1740}
	d4$=mid$("Punter X-ModemCopier Slow P IndexerClock  ",1+pr*7,7):return
{number:1750}
{:1750}
	if a$<>ml$ then ml$=a$:dr=5:gosub {:1009}:a$=dr$+"++ "+a$:&,7,dv%,2
{number:1752}
	an$=a$:&,15,1:d4$=an$:return
{:1800}
{number:1800}
	f4=.:gosub {:1805}:on ec goto {:1801},{:1802},{:1803},{:1804}:x=5:goto {:1980}
{:1801}
{number:1801}
	z$=ef$:f4=1.1:&,28,1:return
{:1802}
{number:1802}
	f4=1.2:&,28,1:return
{:1803}
{number:1803}
	z$=ef$:goto {:1300}
{:1804}
{number:1804}
	a%=asc(ef$+nl$)+256*asc(mid$(ef$,2,1)+nl$):&,42,1
{:1805}
{number:1805}
' password-protected by ep$, and/or costs ec% credits to run command:
' FIXME: where is b% used? can that be removed?
' X-Tec's fix:
'	on -(ec%=.) goto {:1806}:b%=ec%:if ec%>cr then:&"{red}{$ce}ot {$c5}nough {$c3}redits!{f6}":ec=.:return
	if ec then b%=ec%:if ec%>cr then:&"{red}Not Enough Credits!{f6}":ec=.:return
' Pinacolada's change: string variable, subtract credits only after correct password entered
{:1806}
{number:1806}
	p$="Password":if ep$="" or id=1 then return
'	cr=cr-ec%:&"{f6}{white}Password{blue}:{white} ":&,6:if an$=ep$ then return
	&"{f6}{white}{pound}$p{blue}:{white}":&,6:if an$=ep$ then cr=cr-ec%:return
'	&"{f6}{red}Incorrect Password.{f6}":ec=.:return
	&"{f6}{red}Incorrect {pound}$p.{f6}":ec=.
{:1810}
{number:1810}
	return
{:1811}
{number:1811}
	gosub {:1093}:lc=1
{:1812}
{number:1812}
	&,28,1:on tr%+1 goto {:1698}:gosub {:1072}:if lc<>1 then lc=1:gosub {:1093}:sy$=""
' &,52,$19,3: is "Trc right side" (undefined) enabled? (does nothing regardless)
	&,52,$19,3:on a% gosub {:1810}:if pf then a$="GF":goto {:1016}
{:1814}
{number:1814}
	&,28,1:gosub {:1930}:p$=po$:gosub {:1098}:on tr%+1 goto {:1698}:if an$="" then {:1345}
	gosub {:1850}:on f4 goto {:1842}:on rs goto {:1812}
{number:1820}
'	if zz and an$="R" then gosub {:2100}:goto {:1900}
' X-Tec's "Device,Drive" mod:
	if zz and an$="R" then gosub {:2100}:goto {:1890}
' "TEST": loads "image", renumbered/RAM-light version of "im",
' runs Image 2.0's "i.*" modules
	if an$="TEST" then {:1200}
{number:1840}
	x=1:on -(a$="Q")-(a$="{f2}")*2 goto {:1694},{:1345}
{:1842}
{number:1842}
	if f4 then gosub {:2100}:on -(f4=1.1)-2*(f4=1.2)-3*(f4=1.3) goto {:1301},{:1804},{:1696}
{number:1844}
	if rc or val(an$)=.then{:1848}
	a%=val(an$):if a%<1 or a%>4 then an$="":goto {:1848}
	dr=3:a$="e.macs":gosub {:1011}:on e%goto {:1848}:x=id*4-4+a%:gosub {:1001}:&,2,2:an$=a$
	if a$<>"{up arrow}"then uu$=a$:goto {:1814}
{:1848}
{number:1848}
	close 2:gosub {:1989}:goto {:1811}
{:1850}
{number:1850}
	on -(ac%<>ao% )gosub {:1634}:an$=an$+"":rs=1:a$=left$(an$,1):z$=left$(an$,2)
' &,52,20: is "expert mode" enabled?
' &,52, 5: is "pseudo-local" mode enabled?
	&,52,20,3:em=a%:&,52,5,3:zz=a%
	x=1:rq=.:rs=1
{:1860}
{number:1860}
' credit charge or password for using command
	f4=.:a%=zz:b%=2^ac%:&,42:if a% then ef$=b$:ep$=a$:ec=a%:ec%=b%:goto {:1800}
' 1861: log off
	if a$="O" then {:1690}
{number:1869}
	rs=.:return
{:1870}
{number:1870}
' fixing "more prompt - messages only":
' 0=Off, 1=On, 2=Messages Only
' New Image Software:
'	a=1-a:an$=mid$("ffn",a*2+1,2):&"{f6}{pound}$b Mode: O{pound}v7{f6}":return ' 55 bytes
' X-Tec 2018/08/05:
'	a=1-a:a=abs(a):an$=mid$("ffn",a*2+1,2):&"{f6}{pound}$b Mode: {$cf}{pound}v7{f6}":return ' 62 bytes
' Pinacolada 2018/08/06:
	a=abs(1-a):&"{f6}{pound}$b Mode: O"+mid$("ffn",a*2+1,2)+"{f6}":return ' 54 bytes
{:1871}
{number:1871}
	a=val(mid$(uf$,b,1)):gosub {:1870}:uf$=left$(uf$,b-1)+chr$(a+48)+mid$(uf$,b+1)
	return
{number:1880}
' XP - eXPert mode
	a=em:b$="Expert":gosub {:1870}:em=a:&,52,20,2:return
' 1881: PM - Prompt Mode
'	a=pm:b$="Prompt":gosub {:1870}:pm=a:return
' 1882: MP - More Prompt
' closes issue #15
' properly set multi-value "Messages Only" prompt mode. load "im.params"
'	b=2:b$="{f5}More?{f5} Prompt":gosub {:1871}:&,52,34,a:return
	im=3:gosub {:2406}:&,52,34,a:return
' 1883: MN - Menu Mode
	b=3:b$="Menu":gosub {:1871}:&,52,44,a:return
' 1884: commented out (duplicate &,52,44), also Mnu lightbar related. maybe &,52,44,a should be &,52,45,a? -36 bytes
'	b=4:b$="Undefined":gosub {:1871}:&,52,44,a:return
{number:1885}
	poke 970,mp%:&"{f6}":return
{number:1890}
{:1890}
' R - Run a plus-file, X-Tec's mod asks for device/drive:
	poke 53252,14:&"{f6:2}Run: +.{pound}i0":b$=an$:on -(b$="")goto {:1812}:gosub {:1008}:gosub {:1470}
{:1900}
{number:1900}
' update Area window, set cm$ for error logging
' Pinacolada's fix: shorten on-goto
'	cm$=b$:gosub {:1360}:a$=cm$:gosub {:1014}:ON 1-(e%=.)GOTO{:1906},{:3000}
	cm$=b$:gosub {:1360}:a$=b$:gosub {:1014}:on e%+1 goto {:3000}:goto {:1906}
' on 1-(e%=.) goto 1906,3000 ' 19 bytes
' on e%+1 goto 3000:goto 1906 '16 bytes
{:1901}
{number:1901}
	&"{f6}Are You Sure?: "
{:1902}
{number:1902}
	&"{lt. green}[{gray3}No{lt. green}]":gosub {:1007}:&"{pound}h4"
	a=-(a$="Y")
{:1904}
{number:1904}
	a$="No":if a then a$="Yes"
	&:&"{f6}{pound}q0":return
{:1906}
{number:1906}
	pr$="":&:goto {:1812}
{:1907}
{number:1907}
' Yes/No: defaults to Yes
	&"{lt. green}[{gray3}Yes{lt. green}]":gosub {:1007}:a=(a$="N")+1:&"{pound}h5":goto {:1904}
{:1908}
{number:1908}
' &,52,30,3: are main prompt macros enabled? (FIXME: seems superflous here...)
' also called from {:1100} gosub {:1908}
	poke 970,.:&,52,30,3:an$=d1$:&,15:b$=right$(an$,7)
	goto {:1915}
' instead of here... which the SY ECS command points to (forces a read):
	dr=3:a$="e.say":gosub {:1011}:on sgn(e%) goto {:1603}:x=1:gosub {:1001}:input# 2,x
	a=rnd(-ti):&"{f6}":x=int(rnd(1)*x)+2:gosub {:1001}
	input# 2,b$,c$,d$,f$:gosub {:2912}:lp=1:&"{pound}$q{white} {pound}$b{f6}{pound}$q{cyan} {pound}$c{f6}{pound}$q{yellow} {pound}$d{f6}{pound}$q{orange} {pound}$f{f6}{pound}q0":close 2:return
{:1914}
{number:1914}
	poke 53253,.:lp=.:poke 2024,.:poke 2031,.:poke 53260,.:& "{pound}u0{pound}q"+chr$(df%+48):return
{:1915}
{number:1915}
	&"{f6:2} - {pound}$bM":a%=tr%:if a%<100 then:&" - {pound}%a Min. Left":return
	if zz then a%=usr(.):&"{f6} - Stack Free: {pound}%a"
	return
{:1920}
{number:1920}
	&"{f6} Aborted!{f6}":return
{:1930}
{number:1930}
' 30: are main prompt macros enabled?
'	&,52,30,3:on a%gosub {:1931}:return	' 20 bytes
	&,52,30,3:if a%=. then return		' 17 bytes
{:1931}
{number:1931}
	dr=3:a$="e.macros":gosub {:1011}
	if e%=.then:&,2,2:a=val(a$):&"{f6:2}":x=int(rnd(1)*a)+2:gosub {:1001}:&,3,2
	close 2:return
{:1978}
{number:1978}
	x=2
{:1980}
{number:1980}
	&"{f6}":dr=3:a$="e.text":gosub {:1011}:gosub {:1001}:&,2,2:&:close 2:&"{f6}":return
{number:1982}
' directory capacity full?
	b$="Disk Directory"
{:1985}
{number:1985}
	&"{f6}Sorry, {pound}$b Limit Exceeded.{f6}":return
{:1989}
{number:1989}
	&"{f6:2}Unknown Command. Enter {f5}?{f5} For Help.":return
{:1990}
{number:1990}
' BBS node identifiers
	ni=val(an$):n1$=cc$+mid$(str$(ni),2):b$="-"+n1$:n2$=""
	if left$(an$,1)="!" then ni=-1:n1$=mid$(an$,2,3):b$=" @ "+n1$
' 94=^
	an$=mid$(an$,5):&,15,6,94:if an$<>""and ni=-1 then n2$=an$:b$=b$+"/"+n2$
	an$=a$:n0$=a$:&,15,1:an$=an$+b$:return
{:1995}
{number:1995}
	x=6:goto{:1980}
{number:2000}
	poke 22,25:for i=2 to peek(152):close peek(603):next:poke 2031,.
{number:2002}
	x=peek(780):y=peek(781)+peek(782)*256
{number:2004}
	&,11:&,28,1:poke 53248,.
{number:2006}
	dr$=mid$(str$(dr%(5)),2)+":":dv%=dv%(5):load dr$+"im",dv%,2
{:2100}
{number:2100}
	&,28,1:gosub {:2110}:&,27:return
{number:2102}
	goto {:2110}
{number:2104}
' display SB/UD/UX/GF drive info? called from +.t
	im=4:goto {:2426}
' display file transfer info:
	im=5:goto {:2426}
{:2110}
{number:2110}
	dim bb$(31),dt$(61),ed$(61),nn$(61)
	dim a%(61),c%(61),d%(61),e%(31),f%(61),ac%(31),so%(31):return
' FIXME: can this be streamlined?
	lx$=MID$(dt$(a),23):lx$=LEFT$(lx$,LEN(lx$)-2):f2%=a:f1%=.
' TODO: use line 1027's setup:
' 1027 dr=3:a$="e."+b$+",s,"+a$:goto {:1011}
' darkstar:
'	dr=3:z$="e.ss."+LEFT$(lx$,11):a$=z$+",s,r":gosub {:1011}:GET#2,a$:if a$="!" then{:2254}
' pina: "e.ss.123456789ab,s,r"
	b$="ss."+left$(lx$,11):a$="r":gosub {:1027}:&"{up}    {yellow}Comment Status: {rvrs on}"
	if e% then:&"{lt. red} No Comments {rvrs off}"
{:2254}
	&"{up}    {yellow}Comment Status: {lt. green}{rvrs on} Commented {rvrs off}"
	CLOSE 2:CLOSE 1:&"{f6:2}{pound}q0":return
{:2400}
{number:2400}
	a$="misc":goto {:2500}
{:2401}
{number:2401}
	a$="misc2":goto {:2500}
{:2402}
{number:2402}
	a$="editor":goto {:2500}
{:2403}
{number:2403}
	a$="nodes":goto {:2500}
{:2404}
{number:2404}
	a$="local":goto {:2500}
{:2406}
{number:2406}
	a$="params":goto {:2500}
{:2408}
{number:2408}
	a$="comm1":goto {:2500}
{:2410}
{number:2410}
	a$="comm2":goto {:2500}
{:2412}
{number:2412}
	a$="stats":goto {:2500}
{:2414}
{number:2414}
	a$="bar":goto {:2500}
{number:2418}
	a$="screens":goto {:2500}
{:2422}
{number:2422}
	a$="feedback":goto {:2500}
{number:2424}
	a$="trans":goto {:2500}
{:2426}
{number:2426}
	a$="disp":goto {:2500}
{:2428}
{number:2428}
	a$="info":goto {:2500}
{:2430}
{number:2430}
	a$="menus":goto {:2500}
{:2500}
{number:2500}
	gosub {:2510}:is=is+1:im$(is)=im$
{number:2502}
	gosub 60000:new 60000:is=is-1:if is then a$=im$(is):gosub {:2510}
{number:2504}
	return
{:2510}
{number:2510}
	dr=5:gosub {:1009}:im$=a$:a$=dr$+"im."+im$:load a$,dv%
{number:2512}
	return
{number:2600}
' load "+/XX.yyy..." (XX=module name [pr$], yyy...=sub-module name [a$])
	gosub {:2610}:gosub {:4000}:new 4000:return
{:2610}
{number:2610}
	lm$=left$(pr$,2)+"."+a$:dr=5:gosub {:1010}:a$=dr$+"+/"+lm$:load a$,dv%:return
{:2620}
{number:2620}
	gosub {:2630}:gosub 4000:new 4000:return
{:2630}
{number:2630}
' load module in a$, minus "+/" prefix:
	lm$=a$+"":dr=5:gosub {:1010}:a$=dr$+"+/"+lm$:load a$,dv%:return
{:2900}
' MM - memory usage
	a=45:b=43:a$="{f6}Program":gosub {:2910}
	a=47:b=45:a$="Variables":gosub {:2910}
	a=49:b=47:a$="Arrays":gosub {:2910}
	a=51:b=49:a$="Free RAM":gosub {:2910}
	a=55:b=51:a$="Strings":goto{:2910}
	poke 53252,16:&"{f6}Load: {pound}i0{f6}":if an$=""then return
{number:2906}
	dr=5:gosub {:1010}:a$=dr$+an$:load a$,dv%:goto {:2900}
{number:2910}
{:2910}
	c=peek(a)-peek(b)+(peek(a+1)-peek(b+1))*256:&"{pound}$a:{pound}{back arrow}10{pound}!c{f6}":return
{number:2912}
{:2912}
	q$="":on-(ll%<42)goto {:1603}:for r=1 to int((ll%-42)/2):q$=q$+" ":next:return
{number:2999}
{crunch:off}
	copr. 1994 new image 9/21/94
{crunch:on}

{number:3000}
' setup
{:3000}
{step:2}
	poke 828,peek(186):clr:print"{clear}{altdis}{white}{lower}":poke 53280,.:poke 53281,.
	open 131,2,134,chr$(6):poke 248,203:poke 250,204:poke 56,160:poke 52,160
	dim a$,a%,ac%,am,ag$,ak$,am$,an$,ao%
	dim b$,b%,bd,bn$,bu
	dim c$,c%,c1$,c2$,c3$,ca,cc,cc$,cd%,ch$,cm$,cn,co$,co%,cr,ct,ct%
	dim d$,d%,d1$,d1%,d2$,d2%,d3%,d3$,d4$,d5$,d6$,da%,db%,dc,dc%,dd$,dr,dr$,dv%
	dim df%
	dim e$,e%,el,em,ef$,ep$,ec,ec%
	dim f$,f%,f1,f2,f3,f4,ff$,fl,fl$,f1$,f2$,f3$,f4$,f5$,f6$,f7$,f8$
	dim g$,g%
	dim h$,h%,hx$
	dim i$,i%,id,im$,im,is
	dim j$,j%,jn$
	dim k$,k%,kk,kp%
	dim l$,l%,l1,l1$,l2,l2$,l3,l3$,lc,ld$,le,lf,lk%,ll$,ll%,lm$,lp,lt$
	dim m$,m%,mc,mf,ml$,mp$,mt$,mw,mp%,mn%
	dim n$,n%,na$,nc,nf,nl,nl$,nm,nm$
	dim o$,o%
	dim p$,p%,p1%,p2%,p3%,pf,ph$,pl,pm,po$,pp$,pr,pr$,ps,pw$
	dim q$,q%,qb,qe,qt$
	dim r$,r%,rc,rn$,rp,rq
	dim s$,s%,sa%,sh,sg
	dim t$,t%,t1,tc%,tf,tk$,tr%,tt,tt$,tz$
	dim u$,u%,uc,uh,ul,ur,uf$,uu$
	dim v$,v%
	dim w$,w%
	dim x$,x%
	dim y$,y%,y1%
	dim z$,z%,zz
	ac%=16:x=rnd(-ti):hx$="0123456789ABCDEF"+""
	c3$="{f6}Returning To The Editor"+"{f6:2}"
	ak$=" {$c0:39}"+"{f6}"
	r$=chr$(13):nl$=chr$(.):qt$=chr$(34)
	open 4,4,7:poke 836,1:poke 650,128
	poke 56328,.:poke 56579,peek(56579)or 38:poke 56577,peek(56577)or 36
	dim tt$(254),dv%(36),dr%(36),co$(9),hs$(10),bf(6),st(38),im$(5),pf$(10)
	dim ec%(19,24),es%(3,49),ei%(50):restore:for i=1 to 9:read a$:co$(i)=a$+"":next
	dv%=peek(828):z%=dv%:dr$="0"+":":bd$=dr$:sr=2:pr=-1:f3=1:sys 49155:&,53
' FIXME: Boot year is 2000, but better yet would be fix ML to do so
	poke $16ec,$32:poke $16ed,$30:poke $16ee,$30:poke $16ef,$30
' display IM revision (omit time zone):
	gosub {:1336}
	a$="NTSC":if peek(678) then a$="PAL"
	&"{pound}$a System Detected.{f6}"
{:3084}
	close 15:open 15,dv%,15:&,18,.:poke 53248,1:am$=d1$
	a$=dr$+"ml.rs232":&,7,dv%,2:&,16,.
{:3088}
	dv%=z%:&"{f6}{cyan}Reading Config Data...":close 2:close 15:open 15,dv%,15
	fl=.:open 2,dv%,2,bd$+"bd.data,s,r":gosub {:1012}:if e% then close 2:gosub {:3194}:goto{:3088}
	input# 2,dv%(3),dr%(3),po$:close 2:restore:if po$="" then po$="Main: "
{:3094}
	read a$:if a$<>"*" then{:3094}
	gosub {:1196}:gosub {:1063}:for i=1 to 6:x=i+51:gosub {:1001}:&,2,2:dv%(i)=val(a$)
	&,2,2:dr%(i)=val(a$):next:close 2
{:3100}
	&"{f6}Reading System Info..."
	gosub {:1065}:if e% then close 2:gosub {:3194}:goto{:3100}
	x=1:gosub {:1001}:&,2,2:bs$=a$:close 2
	gosub {:1045}:uh=a%:&"{pound}%a "+left$("Users",4-(a%<>1))+"{f6}"
	dr=3:gosub {:1010}
	&"{f6}ECS Defs:":a$=dr$+"e.ecs.main":&,42,4:gosub {:1012}:gosub {:3456}
	&"Print Mode Defs:":a$=dr$+"e.printmodes":&,7,dv%,7:gosub {:1012}:gosub {:3456}
	&"LightBar Defs:":a$=dr$+"e.lightbar":&,7,dv%,8:gosub {:1012}:gosub {:3456}
	&"Alarm Table:":a$=dr$+"e.alarms":&,7,dv%,9:gosub {:1012}:gosub {:3456}
	dr=2:gosub {:1010}
	&"Network Alarm Table:":a$=dr$+"nm.times":&,7,dv%,12:gosub {:1012}:gosub {:3456}
' Pinacolada fix:
' 1027	dr=3:a$="e."+b$+",s,"+a$:goto {:1011}
'	dr=3:a$="e.fkeys,s,r":gosub {:1011}:if e% then{:3132}
	b$="fkeys":b$="r":gosub {:1027}:if e% then{:3132}

	&"Function Keys":for i=1 to 8:&,2,2:if right$(a$,1)="{back arrow}" then a$=left$(a$,len(a$)-1)+r$
	tt$(i)=a$:next:f1$=tt$(1):f2$=tt$(2):f3$=tt$(3):f4$=tt$(4)
	f5$=tt$(5):f6$=tt$(6):f7$=tt$(7):f8$=tt$(8):gosub {:3456}
{:3132}
	close 2:gosub {:1063}:if e% then close 2:gosub {:3194}:GOTO{:3132}
' TODO: Get these in order
	x=1:gosub {:1001}:&,2,2:ca=val(a$):x=12:gosub {:1001}:&,2,2:ur=val(a$)-1
	x=17:gosub {:1001}:&,2,2:d3$=a$:x=18:gosub {:1001}:&,2,2:pp$=a$
	x=19:gosub {:1001}:&,2,2:z$=a$:x=20:gosub {:1001}:input# 2,p1%,p2%,p3%
	x=35:gosub {:1001}:&,2,2:am$=a$:x=36:gosub {:1001}:&,2,2:mc=val(a$)
	x=33:gosub {:1001}:&,2,2:y%=val(a$):x=34:gosub {:1001}:&,2,2:y1%=val(a$)
	x=38:gosub {:1001}:&,2,2:lk%=val(a$)
	df%=3:x=40:gosub {:1001}:&,2,2:a=val(a$):if a>.AND a<16  then df%=a
	x=41:gosub {:1001}:&,2,2:a=val(a$):if a then close 4:OPEN 4,4,a
	x=42:gosub {:1001}:&,2,2:a=val(a$):if a  then poke 17136,a
	x=35:gosub {:1001}:&,2,2:an$=a$:&,15:d6$=an$+"M":x=45:gosub {:1001}:&,2,2:tz$=a$
	x=47:gosub {:1001}:&,2,2:bn$=a$
	x=48:gosub {:1001}:&,2,2:c1$="{clear}{f6}{$07}"+a$+"{f6:2}":x=49:gosub {:1001}:&,2,2:c2$="{f6:2}"+a$+"{f6:2}"
	x=51:gosub {:1001}:&,2,2:cc$=a$
	x=58:gosub {:1001}:&,2,2:nc=val(a$)
	on y% gosub {:3354},{:3364},{:3368},{:3372}:gosub {:3174}
	poke 970,.:poke 971,23:mp$=" ...More? (Y/n) "+"":im=5:gosub {:2428}:gosub {:3202}
	gosub {:1060}:for x=1 to 38:gosub {:1001}:&,2,2:st(x)=val(a$):next:close 2
	an$=am$:lt$=am$:tk$=left$(am$,1):gosub {:3212}
	close 2:gosub {:3188}:&,37:poke 53258,16-(peek(56577) and 16)
	a$=z$:gosub {:1520}:t1=a:an$=z$:&,15:d2$=an$+"M":for i=54272 to i+24:poke i,.:next
	f1=3:&,27,1:a$="lo":goto {:1016}
{:3174}
	a=val(left$(z$,1)):gosub {:3186}:poke 52935,a
	a=val(mid$(z$,2,2)):gosub {:3186}:poke 52938,a
	a=val(mid$(z$,4,2)):gosub {:3186}:poke 52936,a
	a=val(mid$(z$,6,2)):gosub {:3186}:poke 52937,a
	b=val(mid$(z$,8,2)):c=val(mid$(z$,10,2))
	&,62,b,c:b=b+80*((b=92)-(b=12))+12*((b=93)or(b=13)):&,58,b,.:return
{:3186}
	a=16*int(a/10)+a-int(a/10)*10:return
{:3188}
	for q=.to 6:bf(q)=-1:next
	&"{f6}Reading Blocks Free...{f6}":for q=1 to 6:dr=q:gosub {:1079}
	read b$:&"{f6}{rvrs off} {pound}$b Disk:{pound}{back arrow}30{pound}!a":next:q=.:return
{:3194}
	&"{f6:3}{yellow}Insert All System Disks In Correct      Drives, And Press {rvrs on} Return {rvrs off}"
	&" Or, {f6:2}Press (1) To Configure A New Board"
	&"  Or, {f6}Press (2) To Convert From 1.2/1.2a{f6:2}[> ":gosub {:1007}
	&"{f6:2}":on-(an$="1")-(an$="2")*2 goto{:3222},{:3376}:return
{:3202}
	dr=3:a$="e.lightdefs,s,r":gosub {:1011}:if e% then close 2:goto {:3212}
	&"Setting default checks..."
	for x=. to 7:&,2,2:if len(a$)<>16 then{:3210}
	for i=1 TO 16:b$=mid$(a$,i,1):&,52,(x)*16+i-1,val(b$):next
{:3210}
	next:close 2:gosub {:3456}
{:3212}
	a$="e.log"+LEFT$(lt$,1):gosub {:1011}:if e%=. then return
	dr=3:a$="e.log"+LEFT$(lt$,1):gosub {:1023}:an$=d1$:&,15:d6$=an$
	a$=" LOG START: "+d6$:print# 2,a$:gosub {:1096}
	a$=" {pound}q0{yellow}{pound}o{$c0}{pound}o{$c0}"+"{f6}":print# 2,a$:gosub {:1096}:gosub {:1063}:x=44:gosub {:1001}:print# 2,d6$
	close 2:gosub {:1060}:for x=12 to 22:st(x)=.:gosub {:1026}:next:st(20)=.:goto{:1026}
{:3222}
	&"{clear}{f6}{white} Part I: Sysop Information{f6:2}"
	poke 53252,20:w$="IMAGE BBS":b$=w$:pl=1:p$="Sysop Handle":&,1,32
	if an$="" then an$=b$
	tt$(1)=an$:&"{f6}":w$="JOHN":b$=w$:p$="Sysop First Name":&,1,32
	if an$="" then an$=b$
	tt$(3)=an$:&"{f6}":w$="DOE":b$=w$:p$="Sysop Last Name":&,1,32
	if an$="" then an$=b$
	tt$(4)=an$:&"{f6}":w$="[801] 423-2209":b$=w$:p$="Sysop Phone":&,1,32
	if an$="" then an$=b$
	tt$(5)=an$:&"{f6}":w$="DITTO":b$=w$:p$="Sysop Password":&,1,32
	if an$="" then an$=b$
	tt$(2)=an$:tt$=tt$(1)+r$+tt$(2)+r$+tt$(3)+r$+tt$(4)+r$+tt$(5)+r$+d1$+r$
	tt$=tt$+"9"+r$+"0"+r$+"0"+r$+"1"+r$+"5928"+r$
	for i=1 to 10:tt$=tt$+"0"+r$:next:tt$=tt$+d1$+r$
	tt$=tt$+"14111911111111190000"+r$
	&"{clear}{f6} Part II: Device, Drive Configuration{f6:2}":restore:gosub {:3446}
{:3252}
	a=.:read a$:if a$<>"*" then{:3252}
	for i=1 to 6:poke 53252,3:pl=1:read b$
	w$="8":p$=b$+" Device (7-15)":&,1,32:a=val(an$):if a<7 or a>15 then a=8
	dv%(i)=a:w$="0":p$=b$+" Drive (0-254)":&,1,32:a=val(an$):if a<.OR a>254  then a=.
	dr%(i)=a:&"{f6}":next
	&"{clear}{f6} Part III: BBS Information{f6:2}":poke 53252,30:pl=.
	w$="IMAGE BBS V2.0!":b$=w$:p$="BBS Name":&"{f6}":&,1,32
	if an$="" then an$=b$
	bn$=an$:w$="IMAGE MAIN":b$=w$:p$="Main Prompt":&"{f6}":&,1,32
	if an$="" then an$=b$
	po$=an$:w$="* Entering Chat Mode *":b$=w$:p$="Chat Entry":&"{f6}":&,1,32
	if an$="" then an$=b$
	c1$=an$:w$="* Exiting Chat Mode *":b$=w$:p$="Chat Exit":&"{f6}":&,1,32
	if an$="" then an$=b$
	c2$=an$:w$="100":b$=w$:p$="New User Credits":&"{f6}":&,1,32:if an$="" then an$=b$
	nc=val(an$):&"{f6}Creating {f5}bd.data{f5}...."
	a$=bd$+"bd.data,s,w":open 2,dv%,2,a$:print# 2,dv%(3);r$;dr%(3):print# 2,po$
	print# 2,"2.0 Image V2.0 Copyright 1994 New Image Software":close 2:close 15
	gosub {:3454}:&"{f6}Creating {f5}u.config{f5}...."
	dr=6:a$="u.config,l,"+chr$(254):gosub {:1011}
	x=1:gosub {:1001}:print# 2,tt$:x=2:gosub {:1001}:print# 2,"{up arrow}":ur=1:close 2:gosub {:3454}
	&"{f6}Creating {f5}u.index{f5}....":dr=6:gosub {:1010}:a$=dr$+"u.index":print# 15,"s"a$
' insert user #1 into u.index, create a struct:
	a%(0)=1:a%(1)=1:&,60,3,0,a%(0),4,a$,dv%:close 15:gosub {:3454}
	&"{f6}Creating {f5}e.stats{f5}...."
	dr=3:a$="e.stats,l,"+chr$(10):gosub {:1011}
	for x=1 to 40:gosub {:1001}:print# 2,"0":next:close 2:gosub {:3454}
	&"{f6}Creating {f5}e.data{f5}....":ur=2
	dr=3:a$="e.data,l,"+chr$(31):gosub {:1011}:x=1:gosub {:1001}:print# 2,"0"
' "ur" was hard-coded "2":
	for x=2 to 11:gosub {:1001}:print# 2,"{up arrow}":next:x=12:gosub {:1001}:print# 2,ur
	for x=13 to 16:gosub {:1001}:print# 2,"{up arrow}":next:x=17:gosub {:1001}:print# 2,"NOBODY"
	x=18:gosub {:1001}:print# 2,"PASS":x=19:gosub {:1001}:print# 2,d1$
	x=20:gosub {:1001}:print" 0 , 0 , 0":for x=21 to 30:gosub {:1001}:print# 2,"{up arrow}":next
' next available user account:
	x=31:gosub {:1001}:print# 2,ur:x=32:gosub {:1001}:print# 2,"{up arrow}"
	x=33:gosub {:1001}:print# 2,y%:x=34:gosub {:1001}:print# 2,y1%
	x=35:gosub {:1001}:print# 2,d1$:x=36:gosub {:1001}:print# 2,"0"x=37:gosub {:1001}:print# 2,"{up arrow}"
	x=38:gosub {:1001}:print# 2,"0":for x=39 to 42:gosub {:1001}:print# 2,"{up arrow}":NEXT
	x=43:gosub {:1001}:print# 2,"X":for x=44 to 46:gosub {:1001}:print# 2,"{up arrow}":NEXT
	x=47:gosub {:1001}:print# 2,bn$:x=48:gosub {:1001}:print# 2,c1$
	x=49:gosub {:1001}:print# 2,c2$:x=50:gosub {:1001}:b$="0":&,2,2
	if ASC(a$+nl$)<>32  then b$=a$
	gosub {:1001}:print# 2,b$:x=51:gosub {:1001}:print# 2,cc$
	for i=1 to 6:x=51+i:gosub {:1001}:print# 2,dv%(i);r$;dr%(i):NEXT
	x=58:gosub {:1001}:print# 2,nc:close 2:gosub {:3454}
	&"{f6}Creating {f5}e.access{f5}...":dr=3:a$="e.access,l,"+CHR$(127):gosub {:1011}
	for i=.TO 8:x=i+1:gosub {:1001}
	print# 2,"Group"+STR$(i)+r$+"01000100000000021151":NEXT
	x=10:gosub {:1001}:print# 2,"Group 9"+r$+"14111911111111190000"
	close 2:gosub {:3454}:&"{f6}Starting Daily Log...":gosub {:3212}:gosub {:3454}
	&"{f6:2}Be sure to enter {f5}IM{f5} once online to{f6}complete the setup/configuration of{f6}"
	&"your BBS. Message bases, U/D and U/X{f6}libraries can be configured by using{f6}"
	&"the {f5}RE{f5} command from the main prompt.{f6:2}{pound}{back arrow}18New Image Software{f6}":lp=1:return
{:3354}
' FIXME: duplicate
	close 15:OPEN 15,dv%,15:print# 15,"t-ra":&,2,15:close 15
{:3356}
	z$=LEFT$(a$,2):a=-(z$="su")-2*(z$="mo")-3*(z$="tu")-4*(z$="we")-5*(z$="th")
	a=a-6*(z$="fr")-7*(z$="sa"):z$=MID$(STR$(a),2,1)+MID$(a$,12,2)+MID$(a$,6,2)
	a=-80*(MID$(a$,24,1)="p")+val(MID$(a$,15,2))
	z$=z$+MID$(a$,9,2)+RIGHT$("0"+MID$(STR$(a),2),2)+MID$(a$,18,2):GOTO{:3370}
{:3364}
	dr=5:gosub {:1010}:a$=dr$+"swr.ml":&,7,dv%,2:SYS 49155
	z$="":for a=49159 to 49169:z$=z$+CHR$(peek(a)):NEXT:GOTO{:3370}
{:3368}
	z$=am$
{:3370}
	poke 1010,1:return
{:3372}
' FIXME: duplicate
	close 15:OPEN 15,y1%,15:print# 15,"t-ra":&,2,15:close 15:GOTO{:3356}
{:3376}
	REM:close15:open15,dv%,15,"r"+bd$+"d.data=bd.data":close15
	fl=.:&"{f6:2}Reading {f5}d.data{f5}...":close 2:OPEN 2,dv%,2,bd$+"d.data,s,r":gosub {:1012}
	if e% then close 2:&"{f6:2}{f5}d.data{f5} not found!! [CONFIGURE] (Key) {pound}g1":return
	for i=1 to 6:INPUT# 2,dv%(i),dr%(i):NEXT
	&,2,2:cc$=a$:&,2,2:nc=val(a$):&,2,2:&,2,2:&,2,2:bn$=a$:&,2,2:po$=a$:close 2
	&"{f6:2}":gosub {:3446}:&"{f6:2}Converting...":for qq=1 to 4:gosub {:3388}:NEXT:return
{:3388}
	ON qq GOTO{:3390},{:3414},{:3426},{:3434}
{:3390}
	&"{f6}Converting Access Data...":gosub {:1063}
	for x=2 to 11:gosub {:1001}:&,2,2:tt$(x)=a$:NEXT
	for x=21 to 30:gosub {:1001}:&,2,2:tt$(x)=a$:NEXT
	for i=.TO 9:a$=tt$(i+2):tt$(i+2)=MID$(a$,5)
	a=ASC(a$):b=ASC(MID$(a$,2,1)):c=ASC(MID$(a$,3,1)):d=ASC(MID$(a$,4,1))
	if a>100 then a=.
	if b>100 then b=.
	if c>100 then c=.
	if d>100 then d=.
	tt$(i+21)=tt$(i+21)+CHR$(48+c)+CHR$(48+b)+RIGHT$(STR$(100+a),2)+CHR$(48+d)
	NEXT:close 2
	for i=.TO 9:fl$(i)=tt$(i+21):NEXT:&"{f6}Continuing!{f6}":return
{:3414}
	&"Updating {f5}e.data{f5}...":gosub {:1063}:x=32:gosub {:1001}:print# 2,"{up arrow}"
	x=33:gosub {:1001}:print# 2,y%:x=34:gosub {:1001}:print# 2,y1%
	x=35:gosub {:1001}:print# 2,d1$:print# 2,.
	print# 2,.:print# 2,.:print# 2,"3"
	print# 2,"7":x=47:gosub {:1001}:print# 2,bn$:print# 2,"* Entering Chat Mode *"
	print# 2,"* Exiting Chat Mode *":x=51:gosub {:1001}:print# 2,cc$
	for i=1 to 6:x=51+i:gosub {:1001}:print# 2,dv%(i);r$;dr%(i):NEXT
	x=58:gosub {:1001}:print# 2,nc:close 2:&"{f6}Continuing!{f6}":return
{:3426}
	&"Creating {f5}e.access{f5}..."
	close 2:dr=3:a$="e.access,l,"+CHR$(127):gosub {:1011}
	for i=.TO 9:x=i+1:gosub {:1001}
	print# 2,tt$(x+1)+r$+tt$(x+20):NEXT
	x=10:gosub {:1001}:print# 2,tt$(x+1)+r$+"14111911111111190000":close 2
	&"{f6}Done With {f5}e.data{f5}{f6}":return
{:3434}
	&"Writing new {f5}bd.data{f5}{f6}"
	OPEN 2,dv%,2,bd$+"bd.data,s,w":gosub {:1012}:if e%then close 2:gosub {:3194}:GOTO{:3084}
	if po$=""then po$="Main: "
	print# 2,dv%(3);r$;dr%(3):print# 2,po$
	print# 2,"2.0 Image V2.0 Copyright 1994 New Image Software":close 2:close 15
	&"{f6}Conversion Complete!":return
{:3446}
	&"Set Clock From:{f6:2} 1. CMD boot device{f6} 2. Lt.Kernal{f6}"
	&" 3. Last AutoMaint timestamp{f6} 4. CMD external device{f6:2}"
{:3448}
	&"[> ":gosub {:1007}:y%=INT(val(an$)):if y%<1 OR y%>4 then:&"{up}":GOTO{:3448}
{:3449}
' TEST: mention range, offer escape
' NOTE DANGER CAUTION TEST WARNING TESTING haw
	if y%=4 then:&"CMD external device # (8-29): {pound}i1":y1%=int(val(an$))
	if y%=4 then if y1%<8 OR y1%>29 then:&"{f6}":GOTO{:3446}
	return
{:3454}
{:sub.done}
'	&"{pound}{back arrow}33Done.{f6}":return
	b$="Done.":goto {:status.2}

{:sub.status}
' "OK.", "Error!"
	gosub {:1012}:b$="{lt. green}OK.":if e% then b$="{lt. red}Error!{f6}{white}{pound}{back arrow}05{pound}$a"
{:status.2}
	&"{pound}{back arrow}33{pound}$b{pound}q0{f6}":return

{nice:10}
	&"{pound}{back arrow}33{pound}$a{pound}q0{f6}":return
	DATA"Commodore 64","Commodore 128","Amiga","Apple/Comp."
	DATA"IBM/Comp.","Macintosh","Atari/ST","Tandy Series","Other Type"
	DATA"*","System","E-Mail","Etcetera","Directory","Plus-File","User"
	copr. 1994 NEW image 9/21/94
{number:4000}
{:4000}
	&,38:el=y:cm$=cm$+"":p$=p$+"":pr$=pr$+"":im$=im$+"":if x<128 then{:4006}
	a$="FATAL ERROR!!":gosub {:1096}:SYS 64789:print"{clear}{altdis}{white}{lower}{down:2}"a$:END
	&,38:&,61,.,8:print"&,37:goto 1812:":END
{:4006}
	RESTORE
{:4008}
	READ a$:if a$<>"errors"then{:4008}
	&,40:d$="GURU":if x>.AND x<30 then for k=1 to x:READ d$:NEXT
	a%=x:&"{f6}{lt. red} SYSTEM ERROR #{pound}%a {white}({pound}$d ERROR){f6}":r$=CHR$(13)
	a$="im":if el=>3000 then a$="+."+pr$:if el=>60000 then a$="im."+im$
	a=el:&"{lt. blue} In Line:{gray3}{pound}!a {lt. blue}Of {gray3}{pound}$a{f6}"
	&"{f6}{lt. red}Recording Error, Please Wait...":d$=STR$(x)+" ("+d$+" ERROR)"
	b$="errlog":a$="a":gosub {:1027}:if e%then a$="w":gosub {:1027}
	print# 2,na$r$id;r$d1$r$ac%;r$ph$r$rn$r$co$(co%)r$"Error Log"r$
	print# 2,"Error :"d$r$"Line  :"el;r$"Area  : "cm$r$"Prompt: "p$
	print# 2,"Program: +."pr$:print# 2,"Module: +/"lm$:print# 2,"IM Module: im."im$
	print# 2,"ML File: ++ "ml$" ("d4$")"
	for i=1 to 10:a$=CHR$(58-i):if hs$(i)<>""then print# 2," {f8}"a$": "hs$(i)
	NEXT:print# 2,"{up arrow}":close 2
	a$="User  : "+na$+r$+"Error :"+d$+r$+"Line  :"+STR$(b)+r$+"Time  : {$04}"+d1$+r$
	a$=a$+"Pfile : "+pr$+r$+"Area  : "+cm$+r$+".":gosub {:1096}
	close 2:&,37:for x=1 to 4:&,11,x:NEXT:sy$="":sb$="":sg=.
	im$="":is=.:lm$="":pr$="":ml$="":pr=-1:pf=.
	if ac%<>16 then:&,52,4,3:if a%OR(peek(2033)AND 1)=.then{:4050}
	a$="lo":f1=2:GOTO{:1016}
	a$="FATAL ERROR!!":gosub {:1096}:SYS 64789:print"{clear}{altdis}{white}{lower}{down:2}"a$:END
{:4050}
	&,52,4,3:ON-(a%=1 OR i%OR id=1)GOTO{:4068}:gosub {:1065}:x=1:gosub {:1001}:INPUT# 2,i1$
	kk=.:a$="s.errmail,s,r":dr=1:gosub {:1011}:if e%then close 2:GOTO{:4068}
{:4054}
	kk=kk+1:&,2,2:s=(64 AND st):tt$(kk)=a$:if s=.then{:4054}
	close 2:tt$=na$:a$="a":gosub {:1062}:if e%then a$="w":gosub {:1062}
	a$="[System Error ("+d$+")]":a=.:print# 2,i1$r$1;r$d1$r$a$r$
	gosub {:1074}:close 15
	gosub {:1060}:i=1:x=14:gosub {:1025}:x=25:gosub {:1025}:x=32:gosub {:1025}:close 2
	&"{pound}h9{pound}h9{pound}h7ed!{f6:2}{white}{$07}You Have New E-Mail Waiting:{f6:2}{green}Read Now? ":gosub {:1902}
	if a then a$="E"+"M":GOTO{:1016}
{:4068}
	pr$="":GOTO{:1811}
	DATA"errors"
	DATA"TOO MANY FILES","FILE OPEN","FILE NOT OPEN"
	DATA"FILE NOT FOUND","DEVICE NOT PRESENT","NOT INPUT FILE"
	DATA"NOT OUTPUT FILE","MISSING FILE NAME","ILLEGAL DEVICE NUMBER"
	DATA"NEXT WITHOUT FOR","SYNTAX","RETURN WITHOUT GOSUB"
	DATA"OUT OF DATA","ILLEGAL QUANTITY","OVERFLOW"
	DATA"OUT OF MEMORY","UNDEF'D STATEMENT","BAD SUBSCRIPT"
	DATA"REDIM'D ARRAY","DIVISION BY ZERO","ILLEGAL DIRECT"
	DATA"TYPE MISMATCH","STRING TOO LONG","FILE DATA"
	DATA"FORMULA TOO COMPLEX","CAN'T CONTINUE","UNDEF'D FUNCTION"
	DATA"VERIFY","LOAD","BREAK "
	copr. 1994 NEW image 9/21/94
	REM:use this 180508 version with ud comments mod
