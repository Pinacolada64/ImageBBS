{undef:idle_logging}' use x-tec's fix for "e.idle <am$>" debug log
{alpha:alt}
{number:3000}
{crunch:on}
{quoter:32=space} ' make runs of spaces easier to read
{step:2}
	q$="":am=1:k%=.:rs=.:close 2:close 5:on f1 goto {:3256},{:3262},{:3266},{:3333}:goto {:3050}
{:3002}
' &,52,2,3: changing user's access?
' &,52,4,3: pseudo-local mode?
' &,52,6,3: changing user's time remaining?
	&,52,2,3:a=a%:&,52,4,3:a=a+a%:&,52,6,3:a=a+a%:return
{:3004}
	dr=5:gosub 1009:a$=dr$+"+/modem":load a$,dv%
	a$=dr$+"++ clock":&,7,dv%,2:pr=-1:d4$="Clock":goto {:3016}
	return
{:3010}
' increment password mask character per call
	gosub 1063:x=43:gosub 1001:&,2,2:close 2:x=len(a$):if x=. then return
	an$=mid$(a$,cn-int(cn/x)*x+1,1)
	&,15,2:poke 17138,asc(an$):return
{:3016}
	dr=3:a$="e.modem":gosub 1011:if e% then close 2:mt$="0":return
	x=1:if lk%then x=(peek(57092)and 15)+1
	gosub 1001:&,2,2:mt$=a$:close 2:return
	&,22,x*6:return
{:3024}
	a=-((peek(2033)and 128)=128):return
{:3026}
' redirect to 1360. just a quick hack. TODO: change 3026 to 1360
	cm$=a$
	goto 1360
'	&,69,4,21,"               ",1
'	&,69,5,21,a$,1:return
{:3030}
	gosub {:3496}:a$="System Idle":goto {:3026}
{:3032}
	gosub {:3034}:poke 2033,.:return
{:3034}
	b=1:gosub {:3024}:on -(a=.) goto {:3040}:a$="Clearing Line":gosub {:3026}:x=2:goto 4000
{:3036}
	poke 2042,.:poke 53248,1:&"{lt. red}{clear}":return
{:3038}
	poke 53248,.:goto {:3030}
{:3040}
	b$="modem reset":x=11:gosub {:3480}:ti$="000000":x=1:gosub 4000:x=9:goto 4000
{:3042}
	b$="net receive":gosub 2100:x=10:gosub {:3480}:a$="NM/auto":f1=2:goto 1016
{:3044}
	b$="net send":gosub 2100:x=11:gosub {:3480}:a$="NM/auto":f1=1:goto 1016
{:3046}
	&"{f6:2}{pound}q0Sorry, This System Is Reserved For{f6}Network Maintenance.{f6}"
	&"{f6}Please Try Again Later.{f6}":goto {:3266}
{:3050}
	&,52,$22,.:poke 970,.:poke 971,.:gosub {:3036}:poke 53248,.
' calls +/modem: 
	b$="connect":x=11:gosub {:3480}
	poke 2033,.:&"{f6}{pound}b1":&,22,5:gosub 1914:&,52,5,zz:a$="Connected":gosub {:3026}:&,25
	&,52,16,1:&,52,17,1:gosub {:3002}:if a=. then:&,52,4,4
	tr%=4:poke 2042,.:gosub {:3496}:gosub {:3010}:on -(zz=.)goto {:3064}:&,52,$22,.
{:3060}
	nl=1:&,52,16,.:&"{f6:2}Press RETURN for Normal, or{f6}{f5}I{f5} for Instant Logon: {pound}g1"
	on -(an$="I")goto {:3300}:&"Normal{f6}":goto {:3106}
{:3064}
' old: %q BPS CONNECTION CONFIRMED.
	q%=qb:&"{f6:2}CONNECTED AT {pound}%q BAUD.{f6}":gosub 1093:gosub 1372
' &,52,3,3: block 300 baud callers?
	a%=.:if qb<1200 then:&,52,3,3
	tr%=5:if a% then:&"{f6:2}SORRY, NO 300 BPS CALLS ACCEPTED.{f6}":goto {:3266}
' TODO: this would be an interesting feature, but let's comment it out for now
'	rem print#131,"{stop}"chr$(.)chr$(.);:&,22,10:&,4:a=peek(780)and127
'	rem iftr%>.anda=21then3070
'	rem ifa=6thennl=2:&,52,16,.:&"{f6}Image Term Detected.{f6}":goto 3090
{number:3076}
{:3076}
	&"{f6}HIT YOUR BACKSPACE (DELETE) KEY:":&,52,16,.:&"{pound}b1":&,52,16,1
{:3078}
	&,4:a=peek(780)and 127:if (peek(2033)and 128)<>128 or tr%<1 then {:3266}
' &,52,28,3: NetMail enable, received ctrl-a for network transfer:
	&,52,28,3:if a% then if a=1 then {:3042}
	if a=13 then {:3076}
	on -(a<>8 and a<>127 and a<>20) goto {:3078}
	nl=-(a=20):&,52,16,1-nl:b$="ASCII":if nl then b$="Commodore Graphics":&"{pound}k7"
	&"{f6:2}  {rvrs on} {pound}$b {rvrs off}{pound}k0 Mode Detected. {pound}q0{f6:2}":if tr%<1 then {:3266}
	tr%=5:on nl goto {:3096},{:3096}:&,52,18,.:&,52,19,.:&,52,17,1
	&"Do You Want ANSI Color? ":gosub 1907:&,52,18,a:b=a
	&"Do You Want IBM Graphics? ":gosub 1907:&,52,19,a:if a and b then nl=2:poke 951,80
{:3096}
' &,52,41,3: Network reserved?
' &,52,40,3: System reserved?
	&,52,41,3:on a% goto {:3046}:&,52,40,3:if a%=. then {:3106}
	p$="Password":a=.:b$="":if l2=1 then b$="Temporarily "
{:3100}
	&"{f6}System {pound}$bReserved.{f6:2}Enter {pound}$p: ":&,6:if an$=l2$ then {:3104}
	&"{f6:2}Sorry, {pound}$p Invalid.":a=a+1:on -(a<3 and tr%>.) goto {:3100}:goto {:3266}
{:3104}
' fixes single call reservation (l2=1): log it...
	&"{f6:2}{pound}$p Accepted.":on -(l2<>1) goto {:3106}:a$=" Temp Res "+p$+" Cleared":gosub 1093
' on -(l2<>1) goto 3106 = 14
' if l2=1 then (*2) = 12
{number:3105}
' if l2=1 then... makes this line too long to edit on the '64
' ...clear reservation, password, lightbar checkmark, write to e.data:
	l2=.:l2$="^":&,52,40,.:gosub 1063:x=37:gosub 1001:print# 2,l2","l2$:close 2
{number:3106}
{:3106}
	a$="login"+str$(nl):gosub 1350:pw$="":k=0
{:3108}
' &,52,32,3: Enable Mail Check at Logon:
	poke 53252,20:ac%=16:id=.:&,52,32,3:if a%=. then {:3116}
	&"{f6:2}Press RETURN/ENTER to Log On, or{f6}"
	&"{f5}M{f5} to just check mail: {pound}g1":if tr%<1 then{:3266}
	on-(an$="I")-2*(an$="M")goto {:3300},{:3298}:&"Logon{f6:2}"
{:3116}
	&"{f6}Enter Your Handle Or Login ID#{f6:2}{pound}b1:{white} {pound}i1":if tr%<1 then{:3266}
	dd$=an$:on-(dd$="RES")-(dd$="NEW")*2-(dd$="HELP")*3 goto {:3332},{:3162},{:3184}
	if val(an$)then dd$=cc$+an$:goto {:3142}
	i=len(dd$):b$=left$(dd$,1):if left$(dd$,len(cc$))=cc$then{:3142}
	if i<4 or i>20 or b$<"A"or b$>"Z"then id=.:goto {:3160}
	gosub 1046
	if i then id=i:&"{f6}{lt. blue}Your ID# is {white}{pound}!i. {lt. blue}Use It For A{f6}Faster Logon!{f6}":goto {:3144}
' &,52,10,3: Disallow New Users
	&"{f6:2} {f5}{pound}v7{f5}{f6} Not Found.{f6:2}":&,52,10,3:if a%then{:3164}
' Do You Wish To Apply For An
' Account With That Handle?: 
	&"Do You Wish To Apply For An{f6}Account With That Handle?: "
	gosub 1902:if a then f1=1:g$=dd$:dd$="NE"+"W":goto {:3332}
{:3136}
	ac%=16:k=k+1:if k>4 then{:3266}
' immediate mode (i%):
	on i%goto {:3144}:lp=1:&"{f6:2}Enter {f5}NEW{f5} If You Have No Login ID, or {f5}HELP{f5} If "
	lp=1:&"You Are Having Trouble{f6}Logging On.":goto {:3108}
{:3142}
	id=int(val(mid$(dd$,len(cc$)+1,4))):if id<1 or id>ur then id=.:goto {:3160}
{:3144}
	&"{f6}{lt. blue}Please Enter Your Password.{f6:2}{pound}b1:{white} ":&,6:if tr%=.then{:3266}
' immediate mode (i%):
	z$=an$:if z$=""then on i%goto {:3160}:id=.:goto {:3160}
	gosub 1071:if na$="{up arrow}"or na$="RES"then{:3166}
	if i%then on-(pw$=z$)goto {:3170}:goto {:3160}
	&"{f6:2}Additional Information:":a=int(rnd(1)*5)+1:on a gosub {:3174},{:3176},{:3178},{:3180},{:3182}
	if p then:&"{f6}Only fill in the ?'s.{f6}"
	gosub 1006:c$=an$
	if pw$=z$and t$=c$and z$<>""and c$<>""then{:3170}
{:3160}
	&"{f6:2}Login Not Valid.":on-(id<>.and i%=.)gosub {:3216}:goto {:3136}
{:3162}
' &,52,10,3: Disallow New Users
	&,52,10,3:if a%=.then{:3332}
{:3164}
	&"{f6:2}Sorry, No New Users Are Being{f6}Accepted At This Time.{f6:2}":goto {:3108}
{:3166}
	&"{f6:2}That Account Has Been Deleted.{f6}":goto {:3136}
{:3168}
' instant logon (2):
	gosub 1071:if na$="{up arrow}"then{:3060}
{:3170}
	&"{up}Login Complete!{pound}{back arrow}39{f6:2}":IF zz=.THEN k%=i%:i%=.
	goto {:3226}
{:3174}
' TODO: add lightbar checkmark to disable extra login questions
	p$="{f6}LAST Name ":t$=ll$:p=0:return
{:3176}
	p$="{f6}AREA CODE ({rvrs on}???{rvrs off})XXX-XXXX ":t$=mid$(ph$,2,3):p=1:return
{:3178}
	p$="{f6}DIALING PREFIX (XXX)/{rvrs on}???{rvrs off}-XXXX ":t$=mid$(ph$,7,3):p=1:return
{:3180}
	p$="{f6}LAST FOUR DIGITS (XXX)/XXX-{rvrs on}????{rvrs off} ":t$=right$(ph$,4):p=1:return
{:3182}
	p$="{f6}FIRST Name ":t$=ff$:p=0:return
{:3184}
	a$="logon help":gosub 1350
	lp=1:&"{f6:2}Would You Like To Log On With A Temporary Account? ":gosub 1902
	if a=.then tr%=.:goto {:3136}
	&"{f6}Logging On For HELP...{f6}":t=5
{:3192}
	a$="First Name":gosub {:3214}:on-(t<1)goto {:3204}:ff$=an$:if an$=""then{:3192}
{:3194}
	a$="Last Name":gosub {:3214}:on-(t<1)goto {:3204}:ll$=an$:if an$=""then{:3194}
{:3196}
	a$="Phone # or Email":gosub {:3214}:ON-(t<1)goto {:3204}:ph$=an$:IF an$=""THEN{:3196}
	na$="HELP"+"":pw$="NONE"+"":ld$=d1$:ac%=0:dt%=0:tc%=0:co%=1:ll%=40:id=0
	ul=1:lf=1:em=.:dc=.:uc=.:bd=.:bu=.:cr=.:ps=.:rp=.:d5$=d1$:fl$=fl$(ac%)
	co$=co$(co%):rn$=ff$+" "+ll$:tr%=15:gosub 1072:gosub {:3206}:if tr%>.then {:3226}
{:3204}
	&"{f6:2}Sorry, Unable To Log You In...{f6:2}":goto {:3262}
{:3206}
	&"{f6:2}Enter A Brief Message Stating Why You{f6}Need Help.{f6}NOTE: Aborting This "
	&"WILL Log You Off.{f6:2}"
	le=10*(val(mid$(fl$,6,1))+1):gosub 1604:if kk=.then tr%=.:return
	b$="telecheck":ad$="Telecheck":gosub 1068:print# 2,"Forgotten Password"r$:goto 1074
{:3214}
	poke 53252,79:&"{f6}Enter Your {pound}$a:{f6}: {pound}i0":t=t+(an$=""):t=t*-(tr%>.):return
{:3216}
	b$="telecheck":ad$="Telecheck":gosub 1068:tt$=pw$:if id=1 then pw$="Private"
	print# 2,"{lt. green}Failed Security Check. (Attempt"str$(k+1)")"r$
	if z$<>tt$then print# 2,"{lt. green}Password:{yellow}"pw$" {cyan}({f5}"z$"{f5} Was Entered)"r$
	if c$<>t$then print# 2,p$:print# 2,"{lt. green}"t$" {cyan}({f5}"an$"{f5} Was Entered)"r$
	print# 2,"{up arrow}":close 2:return
{:3226}
' instant logon (3):
	a$="on":gosub {:3362}:on f4 goto {:3262}:if i%or k%then{:3250}
	b$=str$(ca):b=7:a=1:if mn%>719 then a=8:b=9:if mn%>1079 then a=17:b=7
	an$=ff$:&,15,1:h$=an$
	gosub 2912:lp=1:g$=mid$("MorningAfternoonEvening",a,b):&"{f6:2}{lt. blue}{pound}$q    {rvrs on} Good {pound}$g, {pound}$h! {rvrs off}{f6}"
	g%=uh:an$=d3$:&,15,1:if d3$=na$then an$="You!"
	f%=cn:a$="s.welcome"+str$(nl):dr=1:gosub 1076:gosub 1910:for i=1 to 10:st(i)=.:next
' 2100: clear vars
	gosub 2100:a$="e.loginmods,s,r":dr=3:gosub 1011:if e%or id<1 then close 2:goto {:3248}
	&,2,2:br=val(a$):if br<1 then close 2:goto {:3248}
	dim lm$(br),lm%(br):rk=.:for i=1 to br:&,2,2:lm$(i)=a$:&,2,2:lm%(i)=val(a$):next
{:3244}
	rk=rk+1:if lm%(rk)and(2^ac%)then a$=lm$(rk):gosub {:3360}
	if rk<br then{:3244}
{:3248}
	&,28,1 ':goto {:3250}
{:3250}
' instant logon (4):
' if user hasn't called before (tc%=1): open e.stats, increment record #8 (new users)
	if tc%=1 then gosub 1060:i=1:x=8:gosub 1025:close 2
	a$="mail":gosub {:3362}:if a then gosub 2100:a$="E"+"M":goto 1016
	goto 1811
' end of instant logon

' log off:
{:3256}
' immediate mode (i%=1) or console login (k%=1?) opt-outs
' &,52,$22 - More Prompt On? assumed ,0 (clear) parameter?
' 1678: leave feedback only if not local or console login:
	&,52,$22:q=i%+k%:if q=. then gosub 1678

' don't read logoff screen if user logged on in instant/console mode:
	if q=. then b$=ff$:&"{f6:2} Bye {pound}$b!{f6:2}":a$="end"+str$(nl):gosub 1350
	if q then:&"{f6}{lt. blue}Update User File?:{white} ":gosub 1902:on a gosub 1070
{:3262}
	a$="Logoff":gosub {:3026}:if ac%=16 then {:3266}
	if i%=. then a$="off":gosub {:3358}
{:3266}
	a=.:gosub 1003:b$="logoff":x=10:gosub {:3480}:&,52,4:&,52,5:gosub {:3004}:gosub {:3032}
	f1=.:&,28,1:gosub {:3496}:if ac%=16 then gosub {:3032}
	ak$=" {$c0:38}"+"{f6}":pf=.:poke 951,40
	gosub 1063:x=17:gosub 1001:&,2,2:d3$=a$:x=19:gosub 1001:&,2,2
' set d2$ (last caller date), get system reservation option (l2), password (l2$)
	an$=a$:&,15:d2$=an$+"M":x=37:gosub 1001:input# 2,l2,l2$
' set user flags + timezone? load "im.info"
	uf$="01000"+tz$:im=6:gosub 2428
	lc=.:em=.:zz=.:gs=.:cf=.:cr=.:lf=1:nl=.:ul=.:le=.:pm=1:c9=.
	gosub {:3002}:if a=.then:&,52,2,4
	i%=.:ac%=16:dc%=.:ct%=.:tc%=.:u$="":sy$="":ba$="":na$="":ph$="":id=.
	poke 830,3:poke 53248,.:&,52,16,1:&,52,17,1:&,52,18,.:&,52,19,.
	&,28,1:gosub 1093:gosub 1914:for j=217 to 242:poke j,peek(j)or 128:next:dr=3:gosub 1079
' this was moved also:
'	gosub 1063:x=37:gosub 1001:input# 2,l2,l2$:close 2
' this was corrected & moved to im.disp
'	if pt%then print"{home}{down}{rvrs on}{white}P{down}{left}R{down}{left}I{down}{left}M{down}{left}E"
{number:3292}
	gosub 1374:gosub {:3496}
	if peek(1010)then a$=d1$:gosub 1520:t1=a
	goto {:3370}
{:3298}
	a$="mailcheck":gosub {:3362}:goto {:3108}
{:3300}
' instant logon (1):
	&"Instant{f6:2}":&,27,2:dim fb%(29,15),ff%(15):dr=3:gosub 1010
	&,60,2,.,fb%(.,.),dr$+"e.sysoplist",dv%:gosub 1012:if e%then{:3318}
' &,52,4,3: Local Mode (No Modem IO)
	&,52,4,3:&,60,8,fb%(.,.),60,2^(a%+3),fb%(11,1),ff%(1),1
	if a%=.then{:3318}
	for i=1 to a%:x=ff%(i):&,60,1,20,fb%(1,x),an$:&,15,1:b%=fb%(.,x)
	&" {white}{pound}!i. {lt. blue}{pound}v7 {cyan}{pound}{back arrow}28(ID# {pound}#0{pound}%b){f6}":next
	poke 53252,4:&"{f6}{lt. blue} Logon Which Account?{white}: {pound}i1"
	a=val(an$):on-(a<1 or a>a%or tr%<1)goto {:3316}:a=ff%(a):id=fb%(.,a):i%=1
	&,28,2:on i%and zz goto {:3168}:goto {:3144}
{:3316}
	&,28,2:goto {:3106}
{:3318}
	id=1:i%=1:&,28,2:goto {:3144}
{:3320}
' set time, 3496: update screen mask
	a$="misc":a=0:gosub {:3358}:gosub {:3496}:goto {:3436}
{:3322}
	&"{clear}":b$="automaint":x=11:gosub {:3480}:a$="automaint":gosub {:3364}:goto {:3336}
{:3324}
' set password
	a$="misc":a=1:gosub {:3358}:goto {:3436}
{:3326}
	a$="misc":a=2:gosub {:3358}:goto {:3436}
{:3328}
	a$="varlist":gosub {:3358}:goto {:3436}
{:3330}
	a$="checknet":gosub {:3358}:goto {:3436}
{:3332}
	a$="new":goto 1016
{:3333}
	on -(e%=1) goto {:3334}:if tr%>. then {:3226}
{:3334}
	ac%=16:goto {:3266}

{:3336}
' start extra automaint modules:
' TODO: move this to +/lo.automaint
	gosub 2100:br=.:a$="e.maintmods,s,r":dr=3:gosub 1011:if e%then {:3352}
	&,2,2:br=val(a$):on-(br<1)goto {:3352}:dim lm$(br),mt$(br)
	for i=1 to br:&,2,2:lm$(i)=a$:&,2,2:mt$(i)=a$:next:close 2:rk=1
{:3342}
	a=val(left$(mt$(rk),3)):z=val(left$(d1$,1)):a=(a and 2^z)<>.or a=.
	a$=mid$(mt$(rk),4,2):b=mid$(d1$,4,2)=a$ or a$="00"
	a$=mid$(mt$(rk),6,2):c=mid$(d1$,6,2)=a$ or a$="00"
	if a and b and c then a$=lm$(rk):gosub {:3356}
	rk=rk+1:if rk<=br then {:3342}
{:3352}
	close 2:&,28,1:am$=d1$+"":gosub 1063:x=35:gosub 1001:print# 2,am$:close 2
	gosub {:3004}:goto {:3266}
' end extra automaint modules

{:3356}
' automaint modules: +/lo/<module>
	a$="/"+a$:goto {:3366}
{:3358}
	gosub {:3364}:goto {:3004}
{:3360}
' login modules: +/lo-<module>
' enough time to run it?
'	a$="-"+a$:on tr%+1 goto {:3368}:goto {:3366}
	a$="-"+a$:if tr% then {:3366}

{:3362}
' if not: "Sorry, Time Limit Exceeded."
	if tr%<1 then b$="Time":goto 1985
{:3364}
' login or system idle modules: +/lo.<module>
	a$="."+a$
{:3366}
' display entire plus-file name in Area window
' 3026 -> 1360, display a$ -> cm$ in Area window:
' lm$ in "e.errlog" assumes +/ prefix
	lm$="lo"+a$:a$="+/"+lm$:gosub {:3026}:dr=5:gosub 1010:a$=dr$+a$:load a$,dv%:gosub 1012:if e%=. then 4000
' TODO: Log failure to "e.errlog":
'{:3367}
'	x=d$=lm$+": "+a$:gosub 1095
{:3368}
	return
{:3370}
	gosub {:3384}:on a goto {:3372},{:3380},{:3372},{:3322},{:3044}
{:3372}
	c=a:a=.:lt$=d1$:poke 53248,.:goto {:3050}
{:3374}
	a$="Ring Detected":gosub {:3026}:goto {:3386}
{:3376}
	a$="Connected":goto {:3026}
{:3378}
	gosub {:3030}:goto {:3388}
{:3380}
	poke 2042,.:poke 53248,1:&"{clear}{f6:2}Loading Image Term{f6}":gosub 2100
	b$="term":x=11:gosub {:3480}:a$="t":goto 1016
{:3384}
	q=.:k=1:x=10:gosub 4000:gosub {:3436}
{:3386}
	t1%=mn%:ti$="000000"
{:3388}
	if ti>36000 then gosub {:3040}
	if mn%<>t1%then t1%=mn%
' &,52,41,3: Network Reserve
' &,52,40,3: System Reserved (Default PW)
	&,52,41,3:z%=a%:&,52,40,3:z%=z%+2*a%:if z%<>kk then gosub {:3496}
' &,52,4,3: Local Mode (No Modem IO)
	&,52,4,3:zz=a%:if zz then x=4:gosub 4000:a=1:return
	if(peek(1010)>.and mid$(am$,2,6)<>mid$(d1$,2,6))then a=4:return
' &,52,29,3: NetMail Trigger
	&,52,29,3:if peek(1010)>. and a% then a=5:return
	poke 646,14:sys 49152:a$="":if peek(198) then get a$
	if a$=""then {:3414}
	poke 198,.
	if a$="R" then b$="reload":x=11:gosub {:3480}:f1=3:pr$="":a$="lo":goto 1016
	x=val(a$):if x then{:3418}
	x=-(a$="V")-2*(a$="C")-3*(a$="+")-4*(a$="-")-5*(a$="T")-6*(a$="{pound}")
' ctrl-f: run terminal
	on-(x<>.)goto {:3420}:x=-(a$="{$06}")-2*(a$="A"):if x then{:3428}
{:3414}
	k=k+(k>.):on-(a$=" " or k=1)goto {:3384}:on -(a$<>"" and (a$<"{f1}" or a$>"{f8}")) gosub {:3454}
{:3416}
	x=5:gosub 4000:on a goto {:3376},{:3374},{:3378}:goto {:3388}
{:3418}
' 7=set reservation password {:3324}
	on x gosub {:3320},{:3438},{:3440},{:3456},1372,{:3040},{:3324},{:3326}:goto {:3416}
{:3420}
	on x gosub {:3328},{:3330},{:3422},{:3424},{:3426},{:3432}:goto {:3416}
{:3422}
	mf=1:a$="m1":x=8:goto 4000
{:3424}
	mf=.:a$="m0":x=8:goto 4000
{:3426}
	poke 53258,16-peek(53258):return
{:3428}
' ctrl-f/ctrl- <- (terminal), shift-T (invert carrier detect)
	on x gosub {:3430},{:3434}:return
{:3430}
	a=2:return
{:3432}
	x=7:gosub 4000:a=.:return
{:3434}
	a=4:return
{:3436}
	poke 53248,.:poke 53252,.:on f3 goto {:3456},{:3438},{:3440}:return
{:3438}
	f3=2:gosub {:3036}:im=2:gosub 2414:goto {:3038}
{:3440}
	f3=3:gosub {:3036}:&"{f6:2} {rvrs on}{white} Blocks Free On System Disks {f6:2}"
	a$="{yellow} {pound}$b Disk:{pound}{back arrow}15{white}{pound}$c{f6}{yellow} {pound}$d Disk:{pound}{back arrow}15{white}{pound}$f{f6}":dr=1
	b$="System":d$="E-Mail":gosub {:3452}
	b$="Etc.":d$="Dir.":gosub {:3452}
	b$="Program":d$="User":gosub {:3452}
	poke 2042,.:&"{f6:2}":goto {:3038}
{:3452}
	c$=str$(bf(dr)):f$=str$(bf(dr+1)):dr=dr+2:&:return
{:3454}
	poke 53248,1:a$="idle":gosub 1350:poke 53248,.:k=50:poke 2042,.:return
{:3456}
	f3=1:gosub {:3036}:poke 2042,1:&"{home}{pound}q0{f6}"
	x%=st(20):w%=st(29):z%=st(23):y%=st(24)
	&"{pound}{back arrow}22{lt. blue}{rvrs on} Feedback   {pound}#3{pound}# {pound}%z {f6}"
	&"{pound}{back arrow}22{lt. blue}{rvrs on} Sysop Mail {pound}#3{pound}# {pound}%y {gray1} {f6}"
	&"{pound}{back arrow}22{lt. blue}{rvrs on} Calls/Log  {pound}#3{pound}# {pound}%x {gray1} {f6}"
	&"{pound}{back arrow}22{lt. blue}{rvrs on} New Users  {pound}#3{pound}# {pound}%w {gray1} {f6}"
	&"{pound}{back arrow}23{gray1}{rvrs on}                {f6}{up}{cyan}"
	dr=3:a$="e.last,s,r":gosub 1011:if e then {:3478}
	for x=1 to 10:&,2,2:on-(len(a$)<11)goto {:3476}:an$=a$:&,15
	&"{f6} "+left$(an$,6)+", "+right$(an$,7)+"M "+mid$(a$,12)
{:3476}
	next
{:3478}
	close 2:goto {:3038}
{:3480}
	p=x:gosub 1060:i=t1:t1=mn%:i=t1-i:i=i-1440*(i<.):st(x)=i:gosub 1026
' "close 2" closes issue #33
	if i then x=x+11:gosub 1025:x=x+16:gosub 1025:close 2
' logs to "e.idle <day>" log, with:
' d1$        "idle" (p=10) 
'         or "used" (p<>10)
'                  i
'                    b$
' 31801168108 used 1 logoff
' 31801168108 idle 0 modem reset
' 31801168108 idle 0 modem reset
' 31801168121 idle 12 modem reset
' 31801168122 idle 0 connect
{ifdef:idle_logging}
' 1068: append or write e.b$:
'	a$="a":gosub {:1027}:if e%then a$="w":gosub {:1027}
' preserve b$ in c$:
	c$=b$:b$="idle "+left$(am$,1):gosub 1068:b$=c$
{else}
' "on i goto..." closes issue #35
	on i goto {:3490}:c$="e.idle "+left$(am$,1)+",s,"
	dr=3:a$=c$+"a":gosub 1011:if e% then a$=c$+"w":gosub 1011
{endif}
	print# 2,d1$","mid$("idleused",1-4*(p=10),4)","i","b$:close 2
{:3490}
	z%=.:y%=.:a=st(21)+st(22)+.5:if a>1 then z%=100*st(21)/a:y%=100-z%
	an$=left$("U="+mid$(str$(z%),2)+" I="+mid$(str$(y%),2)+"     ",10)
	&,69,14,22,an$,$81:return
{:3496}
' turn screen mask on, display date, set reservation password string (None, or password)
	&,18,1:an$=d3$:&,15,1:&,69,5,17,an$,$8c:an$="None":if l2 then an$=l2$
' fix reservation password display (l2$):
	&,69,5,18,d2$,$8c:&,69,5,19,d6$,$8c:&,69,5,20,left$(an$+"{space:21}",21),$8c
' &,52,41,3: Network Reserve
	&,52,41,3:&,69,31,17," Res O"+mid$("ff n  ",a%*3+1,3),$8c:kk=a%
' &,52,40,3: System Reserved (Default PW)
	&,52,40,3:&,69,31,18," Res O"+mid$("ff n  ",a%*3+1,3),$8c:kk=kk+2*a%
' fix reserved display (l2):
' print mid$("NoneOne All ",(l2+1)*4-3,4)
' fixed: 3104 l2, l2$ reset after 1-call reservation
	&,69,32,19,mid$("NoneOne All ",(l2+1)*4-3,4)+"{space:4}",$8c
'	if l2=.then:&,69,31,19," One     ",$8c
'	if l2>.then:&,69,31,19," All     ",$8c
'	&,69,31,20,"         ",1:&,69,31,20,str$(qb),1
'	&,69,31,21,"         ",1:&,69,31,21,str$(uh)+"/"+mid$(str$(ur),2),1
	&,69,31,20,left$(str$(qb)+"         ",9),$8c
	&,69,31,21,left$(str$(uh)+"/"+mid$(str$(ur),2)+"         ",9),$8c
' screen mask, line 22: display c=00000 (total calls to system) and n=000 (calls today)
' check available RAM:
	goto 1374
	rem +.lo - 2018-10-05 pinacolada
