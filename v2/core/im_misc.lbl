{number:60000}
{alpha:alt}
{step:2}
	on im goto {:60006}, {:60026}, {:60004}, {:60046}, {:60062}, {:60068}, {:60074}, {:60078}, {:60082}, {:60088}
	on im-10 goto {:60086}, {:60004}, {:60092}, {:60104}
{:60004}
' im=3
	return
{:60006}
' im=1
	&,40:on-(id=.)goto {:60024}:gosub 1065:uf$=mid$(str$(lf),2,1)+mid$(uf$,2)
	a$=na$+r$+pw$+r$+ff$+r$+ll$+r$+ph$+r$+ld$+r$
	a$=a$+mid$(hx$,ac%+1,1)+mid$(str$(af%),2)+r$+mid$(str$(ct%),2)+r$
	a$=a$+mid$(str$(tc%),2)+r$+mid$(str$(co%),2)+r$
	a$=a$+mid$(str$(ll%+mp%*256),2)+r$+mid$(str$(ul),2)+r$+uf$+r$
	a$=a$+mid$(str$(em),2)+r$+mid$(str$(dc),2)+r$+mid$(str$(uc),2)+r$
	a$=a$+mid$(str$(bd),2)+r$+mid$(str$(bu),2)+r$+mid$(str$(cr),2)+r$
	a$=a$+mid$(str$(ps),2)+r$+mid$(str$(rp),2)+r$+lt$+chr$(48+cd%)+r$+fl$
	x=id:gosub 1001:print# 2,a$:gosub 1001:close 2
{:60024}
	return
{:60026}
' im=2
	gosub 1065:x=id:gosub 1001:input# 2,na$,pw$,ff$,ll$,ph$,ld$,a$
	dd$=cc$+mid$(str$(id),2):rn$=ff$+" "+ll$
	if left$(a$,1)=" "then a$=mid$(a$,2)
	ac%=asc(a$)-48:ac%=ac%+135*(ac%>9):af%=val(mid$(a$,2))
	input# 2,ct%,tc%,co%,ll%,ul,uf$,em,dc,uc,bd,bu,cr,ps,rp,d5$,fl$:close 2
	cd%=.:if len(d5$)>11 then cd%=val(mid$(d5$,12,1)):d5$=left$(d5$,11)
	mp%=int(ll%/256):ll%=ll%-256*mp%:co$=co$(co%)
	if len(uf$)<5 then uf$=mid$(str$(val(uf$)),2,1)+"1000"
	lf=val(left$(uf$,1)):&,52,44,val(mid$(uf$,3,1)):&,52,34,val(mid$(uf$,2,1))
	goto 1072
{:60046}
' im=4
	on peek(53256)goto {:60054}:cc=.:poke 53256,1:&"{f6}{lt. green}Reason For Chat?{f6:2}Chat: {pound}i1"
	on-(an$="")goto {:60060}:a$=" Chat: "+an$:d$=an$:gosub 1095:a$=d$:gosub 1376
	&,52,1,3:&,25,1,a%*255:&,52,.,3:on a%goto {:60052}:gosub {:60054}:goto 1678
{:60052}
	&"{f6}Paging Now!{f6}":return
{:60054}
	cc=cc+1:dr=1:a$="s.chat"+str$(cc):gosub 1076
	if e%then:&,52,33,3:if a%then tr%=.
	return
{:60060}
	poke 53256,.:goto 1920
{:60062}
' im=5
	&"{f6}Time And Date:{f6:2}Logon: {$04}"+lt$:&"{f6}Now:   {pound}v0{f6:2}Minutes Left: "
	a%=tr%:a$="{pound}%a":if a%>99 then a$="Infinite"
	&:return
{:60068}
' im=6
' 1068: append or write "e."+b$
' 1074: write tt$() array elements 1 - (kk-1) to open lfn#2
' 1060: Open "e.stats"
' FIXME: increment which BAR items?
	gosub 1068:gosub 1074:&,11
	gosub 1060:i=1:x=1:gosub 1025:x=12:gosub 1025:x=23:gosub 1025:x=30:gosub 1025
	close 2:return
{:60074}
' im=7
	gosub {:60088}:if i=.then return
	a=5:goto {:60080}
{:60078}
' im=8
	gosub {:60088}:a=4:if i then return
{:60080}
	a%=id:&,16,a:dr=6:gosub 1010:a$=dr$+"u.index":print# 15,"s"a$:&,16,2:return
{:60082}
' im=9
	if pr<>4 then gosub {:60086}:b%=.
	&,16,6:return
{:60086}
' im=11
' load "++ index", open cmd channel, get highest user #?
	a=4:gosub 1736:dr=6:gosub 1010:a$=dr$+"u.index":&,16,1:uh=a%:return
{:60088}
' im=10
' 1065: open u.config
' &,16: search index for handle 'an$', return id 'a%'
	gosub {:60086}:gosub 1065:&,16:close 2
	i=a%:return
{:60092}
' im=13
	if i%=. then:&"{f6}Sorry, Instant Mode Only...{f6:2}":return
{:60094}
	lp=1:&"{f6}Enter the Handle or ID you wish to switch to (RETURN to Abort){f6}"
	p$="Handle":poke 53252,20:gosub 1006:if an$=""then return
	i=val(an$):if i<1 or i>ur then gosub {:60088}:if i=.then:&"{f6}Not found!{f6:2}":goto {:60094}
	gosub 1065:x=i:gosub 1001:&,2,2:if a$="{up arrow}"then:&"{f6}Deleted ID!{f6:2}":goto {:60094}
	id=i:gosub {:60026}:goto 1073
{:60104}
' im=14
' prompt for user to send mail to
' WISH: 1 subroutine for looking up/verifying user name/handle
' WISH: 1 subroutine for emailing user
' check post/respond flag:
	fl=.:a=9:gosub 1004:if a=.then x=4:goto 1980
	x=abs(val(mid$(an$,3))):if x then:&"{f6:2}":goto {:60114}
{:60108}
	p$="":&"{f6:2}{lt. red}Enter User Handle Or ID#.{f6:2}":gosub 1006:x=abs(val(an$)):if x then {:60114}
	on tr%+1 goto {:60148}:on -(an$="") goto {:60148}:gosub 1046:x=i:if x then nn$=an$:goto {:60122}
	&"{f6:2}{white}{f5}{gray3}{pound}v7{white}{f5} {gray3}Not Found.{f6}":goto {:60108}
{:60114}
	i=x:if x<1 or x>ur then{:60118}
	gosub 1065:gosub 1001:input# 2,nn$:if nn$<>"{up arrow}"and nn$<>"RES" then {:60120}
{:60118}
	&"{f6}{yellow}Invalid Account Number.{f6}":x=.:goto {:60148}
{:60120}
	&"{f6}{lt. green}Handle: {rvrs on}"+nn$+"{f6}{yellow}Is That Correct?: ":gosub 1907:if a=. then {:60104}
{:60122}
	y=i:dr=2:gosub 1079:on-(a<20)goto {:60144}:gosub 1085:if a then gosub 1980:goto {:60148}
	on fl goto {:60132}
	poke 53252,30:&"{f6}":&"Subject: ":pl=.:&,1,5:if an$=""then an$="Note":c$=an$
	&"{up}Subject: {pound}v7{f6}"
	tt$(.)="["+an$+"]":gosub 1604:a=1:on tr%+1 goto {:60148}:if kk=. then {:60148}
{:60132}
	&"{f6}Filing {pound}$c For "+nn$+"...{f6}"
	z=1:a$="w":tt$=nn$:gosub 1062:if e%then a$="a":z=.:gosub 1062
	print# 2,na$r$id;r$d1$:for x=.to kk-1:print# 2,tt$(x):next:print# 2,"{up arrow}":close 2
	if y=1 then gosub 1060:x=2:i=1:gosub 1025:x=13:gosub 1025:x=24:gosub 1025:x=31:gosub 1025
	on-(y<>1)gosub 1060:x=3:i=1:gosub 1025:x=14:gosub 1025:x=32:gosub 1025
	x=25:on z gosub 1025:close 2:gosub 1081:goto {:60146}
{:60144}
	x=2:gosub 1980
{:60146}
' WISH: An idea proposed for making a subroutine in im.misc to write emails, given
' WISH: user name, subject, body of email in tt$(), rather than
' WISH: duplicate code in several modules. It needs some consideration, though.
' WISH: If pr$="EM" then this should only ask in E-Mail subsystem, not "+/lo/num squ".
	&"{f6:2}Send to another user? ":gosub 1902:if a then fl=1:goto {:60108}
{:60148}
	return

' don't commit yet
{:sub.get_handle}

	copr. 1994 new image 9/19/94
	rem im.misc
