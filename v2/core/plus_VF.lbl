{number:3000}
{alpha:alt}
{step:2}
	dim fl$(15):f4=.:goto {:3080}
{:3002}
	on bz+1 goto {:3010}:&"{f6}":for x=1 to bz:b%=x:b$=dt$(x):&" {pound}%b. {pound}$b{f6}":next:return
{:3004}
	&,2,2:s=st:on-(a$<>"{up arrow}"and s=.)goto {:3004}:a=a+1:on-(s=.)goto {:3004}:return
{:3006}
	a=abs(int(val(mid$(an$,2,3)))):return
{:3008}
	a=abs(int(val(an$))):return
{:3010}
	return
{:3012}
	for bn=1 to rn:if an$=left$(nn$(bn),1)then z%=a%(bn):n$=bb$(bn):k$=nn$(bn):return
	next:bn=.:return
{:3016}
	a$=mid$(a$,2):b$=mid$(a$,7,2)
	if b$="12"then b$="00"
	if b$="92"then b$="80"
	a$=left$(a$,6)+b$+right$(a$,2):return
{:3024}
	dr=3:a$="e."+n$+",s,r":goto 1011
{:3026}
	gosub 1081:a%(bn)=k%:x=b%(bn):if x then st(x)=k%:gosub 1060:gosub 1026:close 2
	return
{:3030}
	&,2,2:h$=a$:&,2,2:f$=a$:&,2,2:d$=a$:&,2,2:s$=a$:&,2,2:t$=a$:&,2,2:c$=a$
	&,2,2:z$=a$:&,2,2:y$=a$:k=1:a=a+1:if h$=""then h$="None"
{:3034}
	&,2,2:tt$(k)=a$:k=k+1:on-(a$<>"{up arrow}"and peek(144)=.)goto {:3034}:return
{:3036}
	z=1:a$="w":gosub 1062:if e%then z=.:a$="a":gosub 1062
	print# 2,na$r$id;r$d1$r$;:for x=.to kk-1:print# 2,tt$(x):next:print# 2,"{up arrow}":close 2
{:3040}
	gosub 1081:gosub 1060:i=1:x=3:gosub 1025:x=14:gosub 1025:x=32:gosub 1025
	if y=1 then for x=2 to 24 step 11:gosub 1025:next:x=31:gosub 1025
	x=25:on z gosub 1025:close 2:return
{:3046}
	if zz=. then {:3116}
{:3048}
	f1=f1-(f1=.):&"{f6}Which Msg?: {pound}i1":gosub {:3008}:on tr%+1 goto {:3416}:if an$="{f2}"then gosub {:3002}:goto {:3048}
	on-(left$(an$,1)="R")goto {:3060}:if a<1 or a>bz then{:3116}
	k=1:tt$(.)="["+dt$(a)+"]":dr=1:a$="s.can msg"+str$(a)+",s,r":gosub 1011:goto {:3154}
{:3054}
	bz=.:a$="s.can msg,s,r":dr=1:gosub 1011:if e%then close 2:return
	&,2,2:bz=val(a$):if bz=.then close 2:return
	for x=1 to bz:&,2,2:dt$(x)=a$:next:close 2:return
{:3060}
	gosub {:3006}:on-(a<1 or a>bz)goto {:3048}:a$="s.can msg"+str$(a):dr=1:gosub 1076
{:3062}
	dr=3:a$="e.access":gosub 1011:for i=.to 9:x=i+1:gosub 1001:&,2,2:&,2,2:fl$(i)=a$
	next:return
{:3066}
	rn=.:restore
{:3068}
	read a$:on-(a$="{up arrow}")goto {:3074}:rn=rn+1:nn$(rn)=a$:read bb$(rn),b%(rn),a
	if a>.and a<>id and id<>1 then rn=rn-1
	goto {:3068}
{:3074}
	for i=1 to rn:if b%(i)then a=st(b%(i)):goto {:3078}
	dr=3:a$="e."+bb$(i)+",s,r":gosub 1011:a=.:on-(e%=.)gosub {:3004}:close 2
{:3078}
	a%(i)=a:next:return
{:3080}
	&"{clear}{f6}{lt. blue} {rvrs on} View Feedback/Errlog/Telecheck {pound}q0{f6:2}{lt. blue}  Getting {cyan}Messages{lt. blue} And {cyan}Information{lt. blue}..."
	gosub {:3066}:gosub {:3062}:gosub {:3054}
{:3084}
	g%=bz:gosub 1914:f4=.:gosub 2912:lp=1:&"{clear}{f6}{blue}{pound}vj {cyan}{pound}{back arrow}11{pound}$qView Feedback 2.0{pound}{back arrow}39{f6}{blue}{pound}vj{pound}q0{f6}":x=.
	for i=1 to rn:a$=left$(nn$(i),1):b$=mid$(nn$(i),2):a%=a%(i):if x>1 then:&"{f6}":x=.
	&" ({pound}$a){pound}$b{pound}{back arrow}13 {white}({pound}%a){cyan}{pound}{back arrow}18":x=x+1:next
	on-(zz=.)goto {:3100}:if x>1 then:&"{f6}":x=.
	&" (C)an Msg's{pound}{back arrow}13{white} ({pound}%g){pound}{back arrow}18":x=x+1:if x>1 then:&"{f6}":x=.
	&" {cyan}(A)rch. New Users{pound}{back arrow}18":x=x+1:if x>1 then:&"{f6}":x=.
	&" (S)cratch Files{pound}{back arrow}18":x=x+1:if x>1 then:&"{f6}":x=.
' TODO: implement Watchdog files
	&" (W)atchDog Files{pound}{back arrow}18":x=x+1:if x>1 then:&"{f6}":x=.
{:3100}
	&"{f6:2} VF: {pound}g1":gosub {:3012}:if bn then:&"{pound}$k{f6}":goto {:3232}
	if zz then on-(an$="A")-2*(an$="C")-3*(an$="S")-4*(an$="W")goto {:3218},{:3276},{:3176},{:3418}
	&"Quit{f6}":goto {:3416}
{:3106}
	&"{f6}":gosub {:3024}:if e%then:&"No {pound}$k.":close 2:goto {:3084}
	on l goto {:3112}:for i=1 to l-1
{:3110}
	&,2,2:on-(a$<>"{up arrow}"and peek(144)=.)goto {:3110}:next
{:3112}
	gosub {:3030}
{:3114}
	f1=.:f4=.:gosub {:3204}:&,52,12,3:if a%then{:3172}
{:3116}
	gosub 1914:on-(f4=3)goto {:3276}
	b$="":if zz then b$="A)ccs C)an CR)ed D)el V)iew P)rint "
	a%=l:lp=1:&"{f6:2}#{pound}%a:{pound}$bF)wrd R)spd N)ext O)ver Q)uit N)ew #: {white}{pound}i1"
	a=val(an$):if a>.and a<=z%then l=a:close 2:goto {:3106}
	if tr%<1 then an$="Q"
	if an$="O"then on-(f1=2)goto {:3106}:gosub {:3206}:goto {:3116}
	on-(an$="A")-(an$="R")*2-(an$="Q")*3-(an$="F")*4 goto {:3134},{:3158},{:3174},{:3250}
	on-((an$="N")or(an$=""))-(an$="C")*2-(an$="CR")*3 goto {:3172},{:3046},{:3336}
	on-(an$="V")-2*(an$="D")-3*(an$="P")goto {:3370},{:3348},{:3412}:goto {:3116}
{:3134}
	if zz=.then{:3116}
	close 2:f1=f1-(f1=.):&"{f6}{yellow}Access?: {pound}g1{pound}v7{lt. green}{f6}":g=val(an$)
	if an$=r$or val(f$)=.then{:3116}
	gosub 1065:x=val(f$):gosub 1001:for i=1 to 23:&,2,2:ed$(i)=a$:next
	ed$(7)=str$(g):ed$(23)=fl$(val(an$)):a$=""
	for i=1 to 23:a$=a$+ed$(i)+r$:next:gosub 1001:print# 2,a$:close 2
	if n$<>"new"then{:3116}
	&"{f6}{gray3}Send New User Welcome?: ":gosub 1907:if a=.then{:3116}
	k=1:tt$(.)="[Welcome!]":dr=1:a$="s.nu welcome"+str$(g)+",s,r":gosub 1011
	if e%then dr=1:a$="s.nu welcome,s,r":gosub 1011
{:3154}
	if e%then:&:goto {:3116}
{:3156}
	&,2,2:tt$(k)=a$:k=k+1:on-(st=.)goto {:3156}:kk=k:k=1:goto {:3168}
{:3158}
	close 2:f1=2
	&"{f6}Subject?: {pound}i0":if an$=""then an$="RE: "+y$
	&"{up}Subject : {pound}v7{f6}"
	tt$(.)="["+an$+"]":gosub 1604:if kk<1 then{:3116}
	dr=2:gosub 1079:if a<35 then gosub 1978:goto {:3116}
{:3168}
	a%=kk-1:&"{f6}Filing Mail For {pound}$h...":c$=h$:tt$=h$
	y=val(f$):f1=2:gosub {:3036}:&"{f6}":on-(f4=3)goto {:3276}:goto {:3116}
{:3172}
	l=l+1:on t goto {:3174}:on f1+1 goto {:3112},{:3106},{:3106}
{:3174}
	close 2:on-(an$="Q")goto {:3084}:&"{f6:2}End Of {pound}$k.":goto {:3084}
{:3176}
	&"Scratch{f6:2}":x=.:for i=1 to rn:a$=left$(nn$(i),1):b$=mid$(nn$(i),2):if x>1 then:&"{f6}":x=.
	c$=""
	&" ({pound}$a){pound}$b{pound}$c {pound}{back arrow}18":x=x+1:next
	&"{f6:2} (X) All > {pound}g1":on tr%+1 goto {:3416}:if an$="X"then{:3194}
	gosub {:3012}:on-(bn=.)goto {:3084}:&"{pound}$k{f6:2}"
	&"{f6}Scratch {f5}e.{pound}$n{f5}{f6:2}(S)elective or (A)uto: {pound}g1":on tr%+1 goto 1811
	if an$="S"then:&"Selective{f6}":goto {:3388}
	&"Auto{f6}":gosub 1901:on a+1 goto {:3084}:&"{f6}Scratching: e.{pound}$n..."
	dr=3:a$="e."+n$:gosub 1024:k%=.:gosub {:3026}:goto {:3084}
{:3194}
	&"Auto Scratch All!{f6:2}":gosub 1901:if a=.then{:3084}
	&"{f6}Scratching... ":for bn=1 to rn:if a%(bn)=.then{:3202}
	a$="e."+bb$(bn):l=len(a$)-2:&:gosub 1024
	k%=.:gosub {:3026}:&"{pound}h"+mid$(str$(l),2)+"{pound}h2"
{:3202}
	next:&"{white}Done!":goto {:3084}
{:3204}
	t=-(st<>.)
{:3206}
	a%=l:sh=.:&"{clear}{pound}q0{blue}{pound}vj {cyan}#{pound}%a User:{white}{pound}{back arrow}13{pound}$h [#{pound}$f]":s=rc
	&"{f6} {cyan}Date:{white}{pound}{back arrow}13{$04}"+d$+"{f6}{cyan} Access:{white} {pound}$s{pound}{back arrow}13{cyan}Phone:{white}{pound}{back arrow}13{pound}$t":s=s+rc
	&"{f6}{cyan} Name:{white}{pound}{back arrow}13{pound}$c{f6}{cyan} Comp:{white}{pound}{back arrow}13{pound}$z{f6} {cyan}Subj:{white}{pound}{back arrow}13{pound}$y{f6}{blue}{pound}vj{cyan}":s=s+rc
{:3212}
	i=1
{:3214}
	a$=tt$(i):if a$<>"{up arrow}"and rc=.then lp=1:&:i=i+1:goto {:3214}
	&"{pound}q0":return
{:3218}
	an$="N":gosub {:3012}:&"{f6:2}Archive {pound}$k.{f6}":gosub 1901:on a+1 goto {:3084}
	dr=3:gosub 1079:if a<35 then gosub 1978:goto {:3084}
	&"{f6:2}Archiving {pound}$k...":a$="e.nark,s,r":dr=3:gosub 1011:close 2:if e%then{:3228}
	a$="temp":gosub 1024:print# 15,"c"dr$"temp=e.nark,e.new":a$="e.nark":gosub 1024
	print# 15,"r"dr$"e.nark=temp":goto {:3230}
{:3228}
	print# 15,"c"+dr$+"e.nark=e.new"
{:3230}
	gosub 1081:goto {:3084}
{:3232}
	&"{f6:2}Start At (N=New)?: {pound}i1":if left$(an$,1)="N"then{:3238}
	l=val(an$):if l<1 or l>z%then l=1
	goto {:3106}
{:3238}
	&"{f6:2}Scanning For {pound}$k{f6}Since {pound}v1...{f6}":l=.
	a=1:a$=ld$:gosub {:3016}:kk$=a$:gosub {:3024}:if e%then:&"No {pound}$k.":close 2:goto {:3084}
{:3242}
	l=a:gosub {:3030}:&"."
	t=-(st<>.):a$=d$:gosub {:3016}:x=1:if a$<kk$then on 1-t goto {:3242}:x=.
	if x=.then lp=1:&"{f6} No {pound}$k Since {pound}v1. {pound}w1{f6}":goto {:3084}
	goto {:3114}
{:3250}
	close 2:f1=f1-(f1=.):fb=l:&"{f6:2}Forward As E-Mail...{f6}"
{:3252}
	&"{f6}Enter HANDLE Or ID# Of The Person To{f6}Send This Message To.{f6}: {pound}i1"
	if an$=""or an$="{f8}"then{:3116}
	b$=an$:gosub {:3008}:kk=a:on kk+1 goto {:3262}:if kk>ur then{:3116}
	gosub 1065:x=kk:gosub 1001:&,2,2:close 2:if a$="{up arrow}"or a$="RES"then{:3274}
	b$=a$:&"{f6}Is {f5}{pound}$b{f5} Correct?: ":gosub 1902:if a=.then{:3252}
{:3262}
	an$=b$:gosub 1046:y=i
	if i=.then:&"{f6}{f5}{pound}$b{f5} Not Found{f6}In Current User Files.{f6}":goto {:3252}
	tt$(.)="[Forwarded From Feedback]":kk=k:tt$=b$
	z=.:a$="w":gosub 1062:if e%then a$="a":gosub 1062:z=1
	print# 2,h$r$f$r$d1$r$tt$(.)r$"Sent:{$04}"d$:for k=1 to kk-1:print# 2,tt$(k):next
	close 2:gosub {:3040}:&"{f6}":l=fb:goto {:3106}
{:3274}
	&"{f6} {pound}$b Has Been Deleted.":goto {:3116}
{:3276}
	&"{f6:2} (A)dd (E)dit (K)ill (L)ist (S)end? {pound}g1{pound}v7":on tr%+1 goto {:3416}:if an$="S"then{:3322}
	on-(an$="E")-(an$="A")*2-(an$="K")*3-(an$="L")*4 goto {:3282},{:3296},{:3308},{:3280}:goto {:3084}
{:3280}
	on bz+1 goto {:3282}:&"{f6:2} Available Msg's:{f6}":gosub {:3002}:goto {:3276}
{:3282}
	if bz=.then:&"{f6:2}No Messages Exist!{f6:2}":goto {:3276}
{:3284}
	&"{f6:2}Edit Which?: {pound}i1":if an$="{f2}"then gosub {:3002}:goto {:3284}
	bb=val(an$):on tr%+1 goto {:3416}:if bb<1 or bb>bz then{:3276}
	&"{f6}":pl=.:p$="Title":w$=dt$(bb):&,1,32:if an$=""then an$=dt$(bb)
	dt$(bb)=an$:k=1:a$="s.can msg"+str$(bb)+",s,r":dr=1:gosub 1011
	if e%then:&:goto {:3276}
{:3294}
	&,2,2:tt$(k)=a$:k=k+1:on-(st=.)goto {:3294}:kk=k:k=1:gosub 1610:goto {:3302}
{:3296}
	bb=bz+1:if bz>59 then:&"{f6:2}You Are Only Allowed 60 Messages!{f6}":goto {:3276}
	&"{f6:2}Enter The Title of your Message{f6:2}: {pound}i0":on tr%+1 goto {:3416}:if an$=""then{:3276}
	dt$(bb)=an$:gosub 1604
{:3302}
	on-(kk=.)goto {:3276}:&"{f6:2}Filing Your Message.....":bz=bb
	a$="s.can msg"+str$(bb):dr=1:gosub 1023:for k=1 to kk:print# 2,tt$(k):next:close 2
{:3306}
	a$="s.can msg":dr=1:gosub 1023:print# 2,bz:for x=1 to bz:print# 2,dt$(x):next:close 2:goto {:3276}
{:3308}
	if bz=.then:&"{f6:2}No Messages Exist!{f6:2}":goto {:3276}
{:3310}
	&"{f6:2}Kill Which?: {pound}i1":if an$="{f2}"then gosub {:3002}:goto {:3310}
	a=val(an$):if a<1 or a>bz then{:3276}
	b$=dt$(a):&"{f6:2}Killing {pound}$b Msg....{f6}":a$="s.can msg"+str$(a):dr=1:gosub 1024
	for x=a to bz-1:dt$(x)=dt$(x+1)
	dr=1:gosub 1010:print# 15,"r"dr$"s.can msg"str$(x)"=s.can msg"str$(x+1):gosub 1012
	close 15:next:bz=bz-1:goto {:3306}
{:3322}
	&"{f6:2}Enter Handle Or ID# Of User To E-Mail{f6:2}: {pound}i1":a=abs(val(an$)):if a then{:3328}
	gosub 1046:a=i:if a then h$=an$:goto {:3334}
	&"{f6:2}{f5}{pound}v7{f5} {lt. green}Not Found.{f6}":goto {:3276}
{:3328}
	if a<1 or a>ur then{:3332}
	x=a:gosub 1065:gosub 1001:&,2,2:h$=a$:if h$<>"{up arrow}"or h$<>"RES"then{:3334}
{:3332}
	&"{f6}Invalid ID Number.{f6}":x=.:goto {:3276}
{:3334}
	&"{f6}{lt. green}Handle: {pound}$h{f6:2}Is That Correct?: ":gosub 1902:on a+1 goto {:3276}:f4=3:goto {:3046}
{:3336}
	close 2:f1=f1-(f1=.):x=val(f$):if x=.then:&"{f6}No Account!{f6:2}":goto {:3116}
	gosub 1065:gosub 1001:for i=1 to 23:input# 2,ed$(i):next
	&"{f6}":p$="Credits":w$=ed$(19):&,1,32:p$="":if an$=""then{:3116}
	ed$(19)=str$(val(an$)):a$=""
	for i=1 to 23:a$=a$+ed$(i)+r$:next:gosub 1001:print# 2,a$
{:3346}
	close 2:goto {:3116}
{:3348}
	if zz=.then{:3116}
	close 2:f1=f1-(f1=.):x=val(f$)
	if x=.or h$="{up arrow}"or h$="RES"then:&"{f6}No Account!{f6:2}":goto {:3116}
	tt$=h$:&"{f6:2}Delete {f5}{pound}$h{f5}{f6}":gosub 1901:if a=.then{:3346}
	an$=tt$:gosub 1034
	tt$(1)="{up arrow}":tt$(2)="d":for i=3 to 23:tt$(i)="0":next:gosub 1065:a$=""
	for i=1 to 23:a$=a$+tt$(i)+r$:next:gosub 1001:print# 2,a$;:close 2
	if x<l1 then l1=x:x=31:gosub 1063:gosub 1001:print# 2,l1:close 2
	a$="r":gosub 1062:close 2:if e%=.then gosub 1060:i=-1:x=25:gosub 1025:close 2
	dr=2:b$=left$(tt$,13):a$="f."+b$:gosub 1024:a$="m."+b$:gosub 1024
	goto {:3346}
{:3370}
	on zz+1 goto {:3116}:close 2:f1=f1-(f1=.):x=val(f$):if x=.then:&"{f6}No Account!{f6:2}":goto {:3116}
	gosub 1065:gosub 1001:for i=1 to 23:input# 2,ed$(i):next
	b$=ed$(1):c$=ed$(3)+" "+ed$(4):&"{f6:2}Handle{pound}{back arrow}20: {pound}$b{f6}Real Name{pound}{back arrow}20: {pound}$c"
	b$=ed$(5):c$=ed$(7):&"{f6}Phone Number{pound}{back arrow}20: {pound}$b{f6}Access{pound}{back arrow}20: {pound}$c"
	b$=ed$(9):c$=ed$(8):&"{f6}Calls Today{pound}{back arrow}20: {pound}$c{f6}Total Calls{pound}{back arrow}20: {pound}$b"
	b$=ed$(19):c$=ed$(20):&"{f6}Credits{pound}{back arrow}20: {pound}$b{f6}Total Posts{pound}{back arrow}20: {pound}$c"
	b$=ed$(21):&"{f6}Total Responses{pound}{back arrow}20: {pound}$b"
	b$=ed$(16):c$=ed$(18):&"{f6}Total Uploads{pound}{back arrow}20: Files {pound}$b, Blks {pound}$c"
	b$=ed$(15):c$=ed$(17):&"{f6}Total Downloads{pound}{back arrow}20: Files {pound}$b, Blks {pound}$c{f6}":goto {:3116}
{:3388}
	f1=.:k%=.:gosub {:3024}:on-(e%<>.)goto {:3410}:dr=3:gosub 1009:open 3,dv%,3,dr$+"e.temp,s,w":goto {:3392}
{:3390}
	on t+1 goto {:3392}:close 2:close 3:goto {:3404}
{:3392}
	l=k%+1:gosub {:3030}:gosub {:3204}:&"{f6} (D)elete (K)eep (A)bort: {pound}g1":on tr%+1 goto 1811
	if an$="D"then:&"Delete{f6}":&,22,2:goto {:3390}
	if an$="A"then:&"Abort!{f6}":&,22,2:close 2:close 3:a$="e.temp":dr=3:gosub 1024:goto {:3084}
	k%=k%+1:&"Keep{f6}":&,22,2:print# 3,h$r$f$r$d$r$s$r$t$r$c$r$z$r$y$
	for kk=1 to k-1:print# 3,tt$(kk):next:goto {:3390}
	goto {:3390}
{:3404}
	dr=3:gosub 1010:print# 15,"s"+dr$+"e."+n$
	on k%+1 goto {:3408}:print# 15,"r"+dr$+"e."+n$+"=e.temp":close 15:gosub {:3026}:&"{f6:2} {yellow}Done!{f6:2}":goto {:3084}
{:3408}
	dr=3:a$="e.temp":gosub 1024:&"{f6:2}Done!{f6:2}":gosub {:3026}:goto {:3084}
{:3410}
	&"{f6:2} {f5}e.{pound}$n{f5} Not Found.{f6:2}":close 2:goto {:3084}
{:3412}
	if zz then:&"Header also? ":gosub 1902:&,52,12,1:on a+1 gosub {:3212},{:3206}:&,52,12,.
	goto {:3116}
{:3416}
	f1=.:goto 1811
{:3418}
	if id<>1 then{:3458}
{:3420}
	gosub 2912:lp=1:&"{clear}{f6:2}{blue}{pound}vj {cyan}{pound}{back arrow}12{pound}$qWatchDog Files{pound}{back arrow}39{f6}{blue}{pound}vj{pound}q0"
	&"{f6}{cyan} 1.{white} Local Maint File"
	&"{f6}{cyan} 2.{white} E-Mail Sent File"
	&"{f6}{cyan} 3.{white} BlackList LogOn File"
	&"{f6:2}   {lt. blue}=> {pound}g1":a=val(an$):if tr%<1 then{:3458}
	on a goto {:3434},{:3436},{:3438}
	goto {:3458}
{:3434}
	&"{clear}{f6}Local Maint File{f6:2}":b$="zz log":gosub {:3440}:goto {:3420}
{:3436}
	&"{clear}{f6}Email Sent File{f6:2}":b$="em log":gosub {:3440}:goto {:3420}
{:3438}
	&"{clear}{f6}BlackList LogOn File{f6:2}":b$="nu log":gosub {:3440}:goto {:3420}
{:3440}
	a$="e."+b$:dr=3:gosub 1076:if e%then:&"{f6}No Log Found.":return
	&"{f6:2}Delete e."+b$+" ? ":gosub 1902:if tr%<1 then return
	on-(a<>1)goto {:3456}
	dr=3:a$="e."+b$:gosub 1024
{:3456}
	return
{:3458}
	close 15:goto {:3084}
	data"Feedback","fback",23,0
	data"New Users","new",29,0
	data"Error Log","errlog",0,0
	data"Telecheck","telecheck",0,1
	rem data"Add Update","address",0,1
	data"{up arrow}"
	rem "+.VF" copr. 1995 new image 02/11/95
