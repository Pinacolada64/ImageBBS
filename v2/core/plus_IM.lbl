{number:3000}
{step:2}
{crunch:on}
{alpha:alt}
'  Is User in Menu Mode?         44 Mnu 45   Are Menus Available on BBS?
	zz%=8:pr$="":lm$="":&,52,44,3:mm=a%:&,52,45,3:mm=mm and a%:fl=.
{:3002}
	dr=5:gosub 1010:a$=dr$+"++ menu2":&,7,dv%,2:if fl then return
	gosub {:3006}:&,28,1:goto 1811
{:3006}
	&,28,1:n=14:dim m%(16,n),dt$(60),ed$(60),a%(60):if tr%<1 then return
	&,16,.,34,m%(.,1),"A","","Macros Editor"
	&,16,.,34,m%(.,2),"B","","Command Set"
	&,16,.,34,m%(.,3),"C","","Payroll Editor"
	&,16,.,34,m%(.,4),"D","","Logon Editor"
	&,16,.,34,m%(.,5),"E","","Access Groups"
	&,16,.,34,m%(.,6),"F","","File Lists"
	&,16,.,34,m%(.,7),"G","","Function Keys"
	&,16,.,34,m%(.,8),"H","","Lightbar/Alarms"
	&,16,.,34,m%(.,9),"I","","Misc. Features"
	&,16,.,34,m%(.,10),"J","","Modem Config"
	&,16,.,34,m%(.,11),"K","","Time Options"
	&,16,.,34,m%(.,12),"L","","System Drives"
	&,16,.,34,m%(.,13),"M","","Netmail Config"
	&,16,.,34,m%(.,14),"N","","Quit"
	gosub {:3330}:if tr%<1 or lm=n or lm=.then return
	on lm goto {:3188},{:3172},{:3192},{:3068},{:3176},{:3178},{:3190},{:3084},{:3120},{:3174}
	on lm-10 goto {:3264},{:3104},{:3044}
	return
{:3044}
	&,28,1:n=8:dim m%(16,n)
	&,16,.,34,m%(.,1),"A","","Local Info"
	&,16,.,34,m%(.,2),"B","","Modem Specs"
	&,16,.,34,m%(.,3),"C","","Calling Hours"
	&,16,.,34,m%(.,4),"D","","Make nm.create"
	&,16,.,34,m%(.,5),"E","","Connections"
	&,16,.,34,m%(.,6),"F","","Forward List"
	&,16,.,34,m%(.,7),"G","","Netwall/Grf"
	&,16,.,34,m%(.,8),"H","","Quit"
{:3062}
	n=8:gosub {:3330}:if tr%<1 or lm=n or lm=. then {:3006}
	a$="netmail":gosub{:3182}:fl=1:gosub{:3002}:fl=.:goto{:3062}
{:3068}
	&,28,1:n=5:dim m%(16,n),tx$(60),fb%(29,15),a%(60):if tr%<1 then{:3006}
	&,16,.,34,m%(.,1),"A","","Login Mods"
	&,16,.,34,m%(.,2),"B","","Maint Mods"
	&,16,.,34,m%(.,3),"C","","Wall Ads"
	&,16,.,34,m%(.,4),"D","","Quicklist"
	&,16,.,34,m%(.,5),"E","","Quit"
{:3080}
	n=5:gosub {:3330}:if tr%<1 or lm=n or lm=. then {:3006}
	a$="logon":gosub {:3182}:fl=1:gosub {:3002}:fl=.:goto {:3080}
{:3084}
	&,28,1:n=4:zz%=8:dim am%(3,15),m%(16,n),b$(zz%*16-1),c$(60),a%(60)
	&"{f6}Reading Checkmark Definitions...":if tr%<1 then{:3006}
	dr=1:a$="s.lightdefs,s,r":gosub 1011:x=.:if e%then close 2:&:goto {:3092}
	for x=.to 16*zz%-1:&,2,2:b$(x)=a$:next:close 2:&"Done{f6}"
{:3092}
	&,16,.,34,m%(.,1),"A","","LightBar Defs"
	&,16,.,34,m%(.,2),"B","","Alarms"
	&,16,.,34,m%(.,3),"C","","NetMail Alarms"
	&,16,.,34,m%(.,4),"D","","Quit"
{:3100}
	n=4:gosub {:3330}:if tr%<1 or lm=n or lm=. then {:3006}
	a$="lightbar":gosub {:3182}:goto {:3100}
{:3104}
	&,28,1:n=7:zz%=6:dim m%(20,n)
	a=1:a$="System":gosub {:3116}:a=2:a$="E-Mail":gosub {:3116}
	a=3:a$="Etcetera":gosub {:3116}:a=4:a$="Directory":gosub {:3116}
	a=5:a$="Program":gosub {:3116}:a=6:a$="User":gosub {:3116}
	&,16,.,42,m%(.,7),"G","","Quit":n=7:gosub {:3336}:if tr%<1 or lm=n or lm=. then {:3006}
	gosub {:3252}:goto {:3104}
{:3116}
	b$=right$("   "+str$(dv%(a)),3)+","+mid$(str$(dr%(a)),2)
	&,16,.,42,m%(.,a),chr$(192+a),"",left$(a$+" Disk:     ",15)+b$:return
{:3120}
	&,28,1:n=13:dim m%(22,n):gosub 1063:x=38:gosub 1001:&,2,2:lk%=val(a$)
	x=39:gosub 1001:&,2,2:a%(2)=val(a$):x=40:gosub 1001:&,2,2:a%(3)=val(a$)
	x=41:gosub 1001:&,2,2:a%(4)=val(a$):x=42:gosub 1001:&,2,2:a%(5)=val(a$)
	x=43:gosub 1001:&,2,2:mm$=a$:x=45:gosub 1001:&,2,2:tz$=a$
	x=47:gosub 1001:&,2,2:bn$=a$:x=51:gosub 1001:&,2,2:cc$=a$
	x=58:gosub 1001:&,2,2:nc=val(a$):close 2:a$=bd$+"bd.data,s,r":gosub 1011
	input# 2,a$,b$,po$:close 2:restore:for i=1 to 15:read tt$(i):next:tt$(.)=tt$(3)
{:3134}
	&,16,.,46,m%(.,1),"A","","Lt. Kernal Device: "+str$(lk%)
	&,16,.,46,m%(.,2),"B","","Autoweed Cutoff:   "+str$(a%(2))
	&,16,.,46,m%(.,3),"C","","Default Color:      "+tt$(a%(3))
	&,16,.,46,m%(.,4),"D","","Printer Address:   "+str$(a%(4))
	a$=mid$("YesNo",-3*(a%(5)<>10)+1,3)
	&,16,.,46,m%(.,5),"E","","Printer Linefeeds:  "+a$
	&,16,.,46,m%(.,6),"F","","Password Masks:     "+mm$
	&,16,.,46,m%(.,7),"G","","Time Zone:          "+left$(tz$,3):a$=po$
	&,16,.,46,m%(.,8),"H","","Main Prompt:        "+left$(po$,15)
	&,16,.,46,m%(.,9),"I","","BBS Name:           "+left$(bn$,15)
	&,16,.,46,m%(.,10),"J","","System ID           "+cc$
	&,16,.,46,m%(.,11),"K","","New User Credits:  "+str$(nc)
	&,16,.,46,m%(.,12),"L","","Chat Messages"
	&,16,.,46,m%(.,13),"M","","Quit"
	n=13:gosub {:3342}:a=lm:if tr%<1 or lm=n or lm=. then {:3006}
	a$="misc":gosub {:3182}:goto {:3134}
	data"White","Red","Cyan","Purple","Green","Blue","Yellow","Orange","Brown"
	data"Light Red","Dark Gray","Medium Gray","Light Green","Light Blue"
	data"Light Gray"
{:3172}
	a$="ecs":gosub {:3182}:goto {:3006}
{:3174}
	a$="modem":gosub {:3182}:goto {:3006}
{:3176}
	a$="access":gosub {:3182}:goto {:3006}
{:3178}
	a$="filelist":gosub {:3182}:goto {:3006}
{:3180}
	a$="macros":gosub {:3182}:goto {:3006}
{:3182}
	&"{f6}Loading {pound}$a module.."
	if lm$<>"IM."+a$then dr=5:gosub 1010:lm$="IM."+a$:a$=dr$+"+/"+lm$:load a$,dv%
	&"{f6}":goto 4000
{:3188}
	lm=1:goto {:3180}
{:3190}
	lm=2:goto {:3180}
{:3192}
	dr=6:a$="u.payroll,s,r":n=.:gosub 1011:if e% then {:3196}
{:3194}
	n=n+1:&,2,2:tt$(n)=a$:if st=. then {:3194}
{:3196}
	close 2:cf=.
{:3198}
	&"{clear}{f6}{gray3}Payroll Editor{f6}{white}{pound}vj":poke 53252,38
	if n=.then:&"{yellow}No Entries.{f6}":goto {:3206}
	for i=1 to n:a%=i:b%=val(left$(tt$(i),3)):c%=val(mid$(tt$(i),5))
	&"{lt. green}{pound}#2{pound}# {pound}%a{blue}) {lt. green}ID{white}{pound}#0{pound}%b{pound}{back arrow}17{pound}#4{pound}# {pound}%c {lt. green}Credits{f6}":i=i+rc*n:next
{:3206}
	&"{white}{pound}vjA{green}dd{blue}/{white}D{green}elete{blue}/{white}E{green}dit{blue}/{white}Q{green}uit{blue}: {white}{pound}g1"
	on-(an$="A")-2*(an$="D")-3*(an$="E")goto {:3220},{:3230},{:3238}
	&"Quit{f6}":if cf=. then {:3218}
	&"{f6}{white}Save Changes? ":gosub 1907:if a=. then {:3218}
	dr=6:a$="u.payroll":gosub 1023
	for i=1 to n:print# 2,tt$(i):next:close 2
{:3218}
	goto {:3006}
{:3220}
	&"Add{f6:2}"
	poke 53252,4:&"ID: {pound}i1":b%=val(an$):if b%<1 or b%>ur then{:3198}
	poke 53252,5:&"Credits: {pound}i1":c%=val(an$):if c%<1 then{:3198}
	n=n+1:tt$(n)=right$(str$(1000+b%),3)+str$(c%):cf=1
	goto {:3198}
{:3230}
	&"Delete{f6:2}"
	&"Which #? {pound}i1":a=val(an$):if a<1 or a>n then{:3198}
	cf=1:n=n-1:if a=n+1 then{:3198}
	for i=a to n:tt$(i)=tt$(i+1):next:goto {:3198}
{:3238}
	&"Edit{f6:2}"
	&"Which #? {pound}i1":a=val(an$):if a<1 or a>n then {:3198}
	b$=(left$(tt$(a),3)):c$=mid$(tt$(a),5)
	poke 53252,4:w$=b$:p$="ID":pl=1:&,1,32:if an$<>""then b$=an$
	poke 53252,5:w$=c$:p$="Credits":pl=.:&,1,32:if an$<>""then c$=an$
	b%=val(b$):c%=val(c$)
	tt$(a)=right$(str$(1000+b%),3)+str$(c%):cf=1:goto {:3198}
{:3252}
	p$="{gray3}Device{white}":w$=mid$(str$(dv%(lm)),2):&,1,32:a=val(an$)
	if an$<>""and a<>dv%(lm)and a>6 and a<32 then tf=1:dv%(lm)=a
	p$="{gray3}Drive/LU{white}":w$=mid$(str$(dr%(lm)),2):&,1,32:a=val(an$)
	if an$<>""and a<>dr%(lm)and a=>.and a<255 then tf=1:dr%(lm)=a
	if tf then gosub 1063:x=lm+51:gosub 1001:print# 2,dv%(lm);r$;dr%(lm):close 2
	return
{:3264}
	a$="time":gosub {:3182}:goto {:3006}

{:sub.menu_header}
	&"{clear}{blue}{rvrs on}{pound}{back arrow}39{f6}{rvrs on} {cyan}           Image BBS V2.0{pound}{back arrow}37{blue}  {f6}"
	&"{rvrs on} {cyan}        Configuration Editor{pound}{back arrow}37{rvrs off} {rvrs on}{blue} {f6}{rvrs on}  {rvrs off}{pound}{back arrow}38{rvrs on} {f6}"
	return

{:3330}
	gosub {:sub.menu_header}
	&,16,2,34,m%(.,1),n,17,2,n/2+.5,2,6,.:lm=a%+1:&,70,.,n/2+8.5:&"{white}":return

{:3336}
	gosub {:sub.menu_header}
	&,16,2,42,m%(.,1),n,36,1,n,2,6,.:lm=a%+1:&,70,.,n+8:&"{white}":return
{:3342}
	gosub {:sub.menu_header}
	&,16,2,46,m%(.,1),n,36,1,n,2,6,.:lm=a%+1:&,70,.,n+8:&"{white}":return
{crunch:off}
	copr. 1994 new image 10/19/94
	rem 171221 x-tec, 180207 pinacolada
