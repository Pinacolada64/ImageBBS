{alpha:alt}
{crunch:on}
{step:2}
{number:4000}
	&"{f6}{green}{rvrs on}Loading Parameters{rvrs off}{white}...":f4=.:on cd% gosub {:4150}:cd%=1
' 4002
	a$="":for a=1 to ll%-2:a$=a$+"{$c0}":next:ak$=" "+a$+"{f6}"
' 4004
	f1=.:ao%=ac%:gosub 1634
' &,52,20: set Expert mode depending on em
' 4006

	gosub 1063:x=1:gosub 1001:&,2,2:ca=val(a$):close 2:&,52,20,em
' &,52,$22: set More Prompt status (0=off, 1=on)
' NOTE: Goal here is to fix console login (k%) line length
' FIXME: just a test if i% overrides Loc:
' 4008
	poke 970,.:&,52,$22,val(mid$(uf$,2,1)):if i% then:&"i% skips!":qb=2400:goto {:4050}
' &,52,4: Local Mode login
' sysop console login (I think):
' TODO: get highest BPS rate from mt$ for qb?
' 4010
	&,52,4,3:if a%=. then {:4016}
' TODO: if k%=1 then set ll%=40, poke 951 (necessary?)
' 4012
	&"(Sysop Console Login, k%={pound}%k){f6}":qb=2400
' 4014
	rem
{:4016}
' flag 3=Local Maint, handle<>last caller:
	a=3:gosub 1004:if a or na$<>d3$ then {:4018}
' &,52, 21: Disallow Double Calls
' 4018
	&,52,21,3:if a% then:&"{f6}Sorry, No Back-To-Back Calls!{f6}":ac%=16:f4=1:return
{:4018}
' flag 17: calls/day
	a=17:gosub 1004:if left$(d1$,7)<>left$(d5$,7)then ct%=.
	f2=.:ct%=ct%+1:tc%=tc%+1:if ct%<a or a=. then {:4026}
' TODO: buy call for credits, put in credit pool
	if ct%>a then:&"{f6}{lt. blue}Sorry, Call Limit Used Today!{f6}":f4=1:ac%=16:return
	&"{f6:2}{white}Note{blue}:{lt. green} This Is Your Last Call Today.{f6}"
{:4026}
	&"{f6:2}{lt. green}{rvrs on}Logging Call{rvrs off}{white}...":on sgn(id)gosub {:4142}
	dr=3:a$="e.last,s,r":gosub 1011:if e%=.then for i=1 to 10:&,2,2:tt$(i)=a$:next
	close 2:dr=3:a$="e.last":gosub 1023:an$=na$:&,15,1:print# 2,d1$an$
	for i=1 to 9:print# 2,tt$(i):next:close 2
	gosub 1060:i=1:x=20:gosub 1025:close 2
	dd$=cc$+mid$(str$(id),2):jn$="":poke 53252,3:an$=na$:&,15,1:n$=an$
	a$=" {yellow}Caller{white}"+str$(ca)+"   {green}"+dd$+":{white} {rvrs on}"+n$+r$+"{cyan}"+str$(qb)+" Baud, "
	&,52,16,3:b=a%:&,52,18,3:c=a%:b$="{yellow}C/G":if b then b$="{gray3}ASCII":if c then b$="{lt. red}ANSI"
	a$=a$+b$+" Mode"+r$+" {cyan}Logon  {white}{$04}"+d1$
	on-(tc%>1)goto {:4048}:&"New User!{f6}"
	a$=a$+r$+" New User!":if id=.then a$=a$+" (Just Looking)"
{:4048}
	a$=a$+r$+" {lt. red}Credit {white}{rvrs on}"+str$(cr):gosub 1095

{:4050}
' continue immediate mode login
' prime time off, initially
' NOTE: 1003: load "user online" screen mask from im.disp
	f1=.:pt%=.:a=1:gosub 1003
' &,52,7: Prime Time Enabled?
	gosub 1019:&,52,7,3:if a%then pt%=1-pt%:if p1%<1 then p1%=30
	if pt%=. then {:4062}
' prime time enabled: this gets overwritten by screen mask load later. has been moved to im.disp
'	print"{home}{down}{rvrs on}{white}P":print"{rvrs on}{white}R":print"{rvrs on}{white}I":print"{rvrs on}{white}M":print"{rvrs on}{white}E":if g<p1% then {:4062}
' don't disrupt line numbering:
	rem
	if g<p1% then {:4062}
	a%=p1%:tr%=a%
	&"{f6}Note - It Is Now Prime Time -{f6}ALL Callers Are Limited To {pound}%a Minutes!{f6:2}"
{:4062}
	&"{f6}":cn=cn+1:gosub 1073
' flag 17: calls per day (0=unlimited, 1-9)
	a=17:gosub 1004:if a then a%=a-ct%:&"{f6}{lt. green}Calls Left Today{blue}:{white} {pound}%a{f6}"
{:4066}
' flag 18: Time/Call is 2 bytes
	a%=val(mid$(fl$,18,2)):if a%>.then:&"{f6}{lt. green}Time Allowed This Call{blue}:{white} {pound}%a Min.{f6:2}"
' set up current user's time zone (im=6),
' otherwise ask new user's time zone, if TZ flags missing/BBS is converted from 1.2 (im=4)
	tr%=a%-101*(a%=.):im=6:if len(uf$)<11 then im=4
' load "im.info"
	gosub 2428
{:4072}
	on -(mp%>. and mp%<>40 and mp%<>80) goto {:4078}:a$="lines/screen":gosub 1350
	lp=1:&"{f6:2}{lt. green}How Many Lines Of Text Can Your Screen Display?: {pound}i1"
	mp%=abs(val(an$))-25*(tr%=.):if mp%<1 or mp%>255 or mp%=40 or mp%=80 then mp%=.:goto {:4072}
{:4078}
	a$=left$("f."+na$,16)+",s,r":dr=2:gosub 1011:on e%goto {:4134}
{:4080}
' TODO: Move this to "+/lo.forced mail"?
	f4=.:&"{f6}":&,2,2:c$=b$:b$=a$:if a$="{up arrow}"or st<>. then {:4088}
	fl=-(a$="ERASE"or a$="KILL"or a$="OFF"):on fl goto {:4080}
	fl=-(left$(a$,1)="!"or left$(a$,1)="{back arrow}"or left$(a$,1)="@"):on fl goto {:4080}
	&:goto {:4080}
{:4088}
	a$="KILL":if b$=a$or c$=a$then:&"{f6:2}Terminating {pound}v2's Account...{f6}":gosub {:4116}
	a$="!":if left$(b$,1)=a$then b$=mid$(b$,2):gosub {:4108}
	a$="!":if left$(c$,1)=a$then b$=mid$(c$,2):gosub {:4108}
	a$="OFF":if b$=a$or c$=a$then f4=1
	a$="{back arrow}":if left$(b$,1)=a$then ac%=val(mid$(b$,2)):fl$=fl$(ac%)
	a$="{back arrow}":if left$(c$,1)=a$then ac%=val(mid$(c$,2)):fl$=fl$(ac%)
	a$="ERASE":if b$=a$or c$=a$then gosub {:4110}
	a$="@":if left$(b$,1)=a$then nn$=mid$(b$,2):gosub {:4136}
	a$="@":if left$(c$,1)=a$then nn$=mid$(c$,2):gosub {:4136}
	goto {:4134}
{:4108}
	if mid$(d1$,2,6)<mid$(b$,2,6) then {:4112}
{:4110}
	dr=2:gosub 1010:print# 15,"s"dr$"f."left$(na$,13):gosub 1081:goto {:4134}
{:4112}
	an$=b$:&,15:b$=mid$(an$,1,12):&"{f6:2}{lt. red}Sorry, Your Account Is Suspended"
	&" Until{f6}{pound}$b...{f6}":a$=" Account Suspended!":gosub 1095:goto {:4128}
{:4116}
	&,52,4,3:on-(a%=1 or id=1)goto {:4134}:an$=na$:tt$=an$:gosub 1034:tt$(1)="{up arrow}":tt$(2)="d":for i=3 to 24:tt$(i)="0":next
	gosub 1065:a$="":for i=1 to 23:a$=a$+tt$(i)+r$:next:x=id:gosub 1001:print# 2,a$:close 2
	if x<l1 then l1=x:x=31:gosub 1063:gosub 1001:print# 2,l1:close 2
	a$="r":gosub 1062:close 2:if e%=.then gosub {:4134}:i=-1:x=25:gosub 1025:close 2
	dr=2:d$=left$(tt$,14):a$="f."+d$:gosub 1024:a$="m."+d$:gosub 1024:gosub 1081
	a$=" User Terminated!":gosub 1095
{:4128}
	an$=d1$:&,15:a$=a$+" {cyan}Logoff {white}"+an$+"M"+r$
	a$=a$+" {pound}q0{yellow}{pound}o{$c0}{pound}o{$c0}":gosub 1095
	ac%=16:f4=1
{:4134}
	close 2:return
{:4136}
	an$=na$:gosub 1034:dr=2:gosub 1010:print# 15,"r"dr$"f."left$(nn$,14)"=f."left$(na$,14)
	print# 15,"r"dr$"m."left$(nn$,14)"=m."left$(na$,14):an$=nn$:gosub 1038:na$=nn$
	gosub 1073:gosub 1070:goto 1081
{:4142}
	a=4:gosub 1736:dr=6:gosub 1010:a$=dr$+"u.weedinfo":&,16,1:gosub 1012
	if e%then:&,16,3:b%=1:goto {:4148}
{:4146}
	a%=id:&,16,8:if a%then:&,16,5:goto {:4146}
{:4148}
	a%=id:&,16,4:a$=dr$+"u.weedinfo":print# 15,"s"a$:&,16,2:return
{:4150}
	an$=d5$:&,15:lp=1:&"{f6:2}At the end of your last call, on {pound}v7M, you "
	lp=1:&"didn't properly log off. Do you want your message scan date set to "
	lp=1:&"that time? ":gosub 1907
	if a then ld$=d5$
	return
{crunch:off}
	rem +/lo.on - 12/20/2017 - prime time display fix
	copr. 1994 new image 11/14/94
