' print fre(0) - (fre(0)<0) * 65536
{loadaddr:$c000}
{asm}
;image 2.0 equates
;
var = $61
chrget = $73
chrgot = $79
ridbe = $29b
ridbs = $29c
;
ten = $dc08
scs = $dc09
min = $dc0a
hrs = $dc0b
;carrier = $dd01
;
getin = $ffe4
chkout= $ffc9
chrout= $ffd2
clrchn= $ffcc
chkin = $ffc6
setnam= $ffbd
setlfs= $ffba
loadf = $ffd5
savef = $ffd8
readst= $ffb7
colors= $e8da
chrin = $ffcf
setmsg= $ff90
openf = $ffc0
closef= $ffc3
;
poketok= 151
peektok= 194
systok = 158
loadtok= 147
newtok = 162
;
linkprg=$a533
makerm1=$b475
getbytc=$b79b
ptrget1=$b0e7
synerr =$af08
;chrget =$73
getnum =$b7eb
ilqerr =$b248
;chrgot =$79
retval =$bc49
parchk =$aef1
getadr =$b7f7
retbyt =$b3a2
gone1 = $a7e7
gone2 = $a7ea
eval1 = $ae8d
frmnum =$ad8a
syscll =$e130
comma = $aefd
getfile=57812
linget = $a96b
error = $a437
chkcom= $aefd
;
jmptbl = $a000
outastrp= $cd00
usetbl1 = $cd03
swapper = $cd06
swapagn = $cd09
trace = $cd0c
chkspcl = $cd0f
convchr = $cd12
buffer = $ce77
buf2 = $ce27
;
bsnpre = $02fd
bsnval = $02fe
;
death= $1800
prgstart = $1801
bmpstart = $2001
mmpstart = $3001
lmpstart = $4001
prgend = $5000
imstart = $5001
protostart = $c000
protoend = $ca80
;
mainline = 1000
trapline = 2000
imodline = 2300
immdline = 3000
immdsize = $a00
;
tdisp = $0400 + 960
tcolr = $d800 + 960
sdisp = $0400 + 920
scolr = $d800 + 920
ldisp = $0400 + 640
lcolr = $d800 + 640
adisp = $0400 + 880
acolr = $d800 + 880
;
fkeybuf = 679 ;32
emptym0 = 711 ;57
;
cassbuff = 828
idlemax =830
datebuf = 831 ;11
daytbl = 842 ;24
tzoneh = 866
tzonem = 867
uzoneh = 868
uzonem = 869
wrapbuf = 870 ;80
wrapflg = 950
modclmn = 951
ptrclmn = 952
wrapind = 953
wrapdmp = 954
ptrclm = 955
modclm = 956
sndtim1 = 957
sndtim2 = 958
sndwav1 = 959
sndwav2 = 960
sndwav3 = 961
sndrept = 962
sndtim1a= 963
sndtim2a= 964
jiffy = 965
blnkflag =966
blnkcntr =967
ptrlin = 968
ptrlinm= 969
usrlin = 970
usrlinm= 971
fredmode= 972
montbl = 973 ; 36
emptym1= 1009 ;1
timeset= 1010
flag1 = 1011
case1 = 1012
temp3 = 1015
mline = 1016
comm = 1017
flags = 1018
cline = 1019
lines = 1022
modes = 1023
;
gc = $c000
gchide = $e000
gclen = 4
;
ecs = $c000
ecshide = $e400
ecslen = 10
;
;screen parameters
;
mcolor = 646
mreverse = 199
curptr = 209
scnpos = 211
sline = 214
colptr = 243
undchr = 206
undcol = 647
crsrflg = 204
crsrmode = 207
scnclm = scnpos
;
mjump =$07e8
mresult=$07e9
mspeed =$07ea
mprint =$07eb
;mcolor =$07ec
;mprtr =$07ed
;mreverse=$07ee
mci = $07ef
mdigits = $07f0
carrst = $07f1
tsp1 =$07f2
tsp2 =$07f3
chks =$07f4
readmode = $07f6
filenum = $07f7
tmp5 = $07f8
abtchr = $07f9
clock = $07fa
filetyp = $07fb
tmp1 =$07fc
tmp2 =$07fd
tmp3 =$07fe
tmp4 =$07ff
;
local = $d000
case = $d001
editor = $d002
;tsr =$d003
llen = $d004
flag = $d005
chat = $d006
inchat = $d007
chatpage = $d008
carrier = $d009
mxor = $d00a
mkolor = $d00b
mupcase = $d00c
irqcount= $d00d
trans = $d00e
; index = $d00f
;
tempscn = $1000 ;$140
tempscn0 = tempscn+000
tempscn1 = tempscn+040
tempscn2 = tempscn+080
tempscn3 = tempscn+120
tempscn4 = tempscn+160
tempscn5 = tempscn+200
tempscn6 = tempscn+240
tempscn7 = tempscn+280
tempcol = $1140 ;$140
tempcol0 = tempcol+000
tempcol1 = tempcol+040
tempcol2 = tempcol+080
tempcol3 = tempcol+120
tempcol4 = tempcol+160
tempcol5 = tempcol+200
tempcol6 = tempcol+240
tempcol7 = tempcol+280
emptym3 = $1280 ;96
;
idlejif = $12e0
idlesec = $12e1
idleten = $12e2
idlemin = $12e3
curdsp = $12e4
bar = $12e5
tsr2 = $12e6
mright = $12e7
mleft = $12e8
cphase = $12e9
key = $12ea
shft = $12eb
ptrlnfd = $12f0
ha577 = $12f1
mask = $12f2
scnmode = $12f3
dflag = $12f4
dstat = $12f5
cytmp = $12f6
interm = $12f7
cxsav = $12f8
len1 = $12f9
passmode = $12fa
scnlock = $12fb
tmp6 = $12fc
tmp7 = $12fd
freq = $12fe
;
rs232 = $0800
rsinabl = rs232 + $03
rsdisab = rs232 + $06
rsget = rs232 + $09
rsout = rs232 + $0c
rsbaud = rs232 + $0f
rschar = rs232 + $12
;
chktbl = $1300 ;$10
bartbl = $1310 ;$c0
arryptrs =$13d0 ;$10
daysofm = $13e0 ;$0c
emptym2 = $13ec ;$04
sndtbl = $13f0 ;$60
netalrm = $1450 ;$30
tblatc = $1480 ;$80
tblcta = $1500 ;$100
tblcta1 = $1600 ;$20
tblcta2 = $1620 ;$20
tblcta3 = $1640 ;$20
alarmtb = $1660 ;$80
date1 = $16e0 ;$20
lobytes = $1700 ;25
hibytes = $1719 ;25
lobytec = $1732 ;25
hibytec = $174b ;25
emptym4 = $1764 ;28
pmodetbl= $1780 ;$80
;
ribuf = $0b00
robuf = $0b80
;
wedgemem = $0c00
trapoff = wedgemem+0
trapon =  wedgemem+3
loadprg = wedgemem+6
arraysav = wedgemem+9
arrayres = wedgemem+12
forcegc = wedgemem+15

;
; image 2.0 garbage collection
;
hi = 51
size = 128
;
; setup gc parameters

initgc:		lda #$c7	; G
			sta tdisp+31
			lda 3
			pha
			lda 4
			pha
			lda 5
			pha
			lda 6
			pha
			lda 7
			pha
			lda 8
			pha
			lda 49
			ldx 50
			sta lo
			stx lo+1
			lda 55
			ldx 56
			sta hi
			stx hi+1
gc0:		lda #0
			sta index
; loop thru scalars
			lda 45
			ldx 46
			sta 3
			stx 4
gc1:		lda 3
			cmp 47
			bne gc1a
			lda 4
			cmp 48
			beq gc4
gc1a:		ldy #0
			lda (3),y
			bmi gc2
			iny
			lda (3),y
			bpl gc2
			lda #2
			jsr add3
			ldy #0
			jsr dostr
			lda #5
			jsr add3
			jmp gc1
gc2:		lda #7
			jsr add3
			jmp gc1
; loop thru arrays
gc4:		lda 3
			cmp 49
			bne gc4a
			lda 4
			cmp 50
			beq gc6
gc4a:		ldy #0
			lda (3),y
			bmi gc5
			iny
			lda (3),y
			bpl gc5
			iny
			jsr doarray
gc5:		ldy #2
			clc
			lda 3
			adc (3),y
			tax
			iny
			lda 4
			adc (3),y
			stx 3
			sta 4
			jmp gc4
gc6:		lda index
			beq gc7
			jsr collect
			lda index
			cmp #size
			beq gc0
gc7:		pla
			sta 8
			pla
			sta 7
			pla
			sta 6
			pla
			sta 5
			pla
			sta 4
			pla
			sta 3
			lda #$a0
			sta tdisp+31
			rts
;
add3:		clc
			adc 3
			sta 3
			bcc add3a
			inc 4
add3a:		rts
;
doarray:	clc
			lda (3),y
			adc 3
			sta 7
			iny
			lda (3),y
			adc 4
			sta 8
			lda 3
			pha
			lda 4
			pha
			iny
			lda (3),y
			asl
			clc
			adc #5
			jsr add3
doarray1:	lda 3
			cmp 7
			bne doarray2
			lda 4
			cmp 8
			beq doarray3
doarray2:	ldy #0
			jsr dostr
			clc
			lda 3
			adc #3
			sta 3
			bcc doarray1
			inc 4
			jmp doarray1
doarray3:	pla
			sta 4
			pla
			sta 3
			rts
;
dostr0:		rts
dostr:		lda (3),y
			tax
			iny
			lda (3),y
			sta 5
			iny
			lda (3),y
			sta 6
			cpx #0
			beq dostr0
			lda 5
			cmp lo
			lda 6
			sbc lo+1
			bcc dostr0
			lda 5
			cmp hi
			lda 6
			sbc hi+1
			bcs dostr0
			ldx index
			beq dostr4
			ldx #0
dostr1:		lda 6
			cmp tbldh,x
			bcc dostr6
			bne dostr2
			lda 5
			cmp tbldl,x
			bcc dostr6
			beq dostr5
dostr2:		stx temp
			cpx #size-1
			beq dostr4
			ldx #size-1
dostr3:		lda tblpl-1,x
			sta tblpl,x
			lda tblph-1,x
			sta tblph,x
			lda tbldl-1,x
			sta tbldl,x
			lda tbldh-1,x
			sta tbldh,x
			dex
			cpx temp
			bne dostr3
dostr4:		lda 3
			sta tblpl,x
			lda 4
			sta tblph,x
			lda 5
			sta tbldl,x
			lda 6
			sta tbldh,x
			lda index
			cmp #size
			beq dostr5
			inc index
dostr5:		rts
dostr6:		inx
			cpx index
			bcc dostr1
			cpx #size
			bcc dostr4
			rts
;
collect0:	rts
collect:	ldx #0
collect1:	cpx index
			beq collect0
			lda tblpl,x
			sta 3
			lda tblph,x
			sta 4
			lda tbldl,x
			sta 5
			lda tbldh,x
			sta 6
			stx temp
			ldy #0
			lda (3),y
			tax
			sec
			lda hi
			sbc (3),y
			sta hi
			lda hi+1
			sbc #0
			sta hi+1
			txa
			tay
			dey
collect2:	lda (5),y
			sta (hi),y
			dey
			dex
			bne collect2
			ldy #1
			lda hi
			sta (3),y
			iny
			lda hi+1
			sta (3),y
			ldx temp
			inx
			jmp collect1
;
index:		byte 0
temp:		byte 0
lo:			word 0
tblpl:		area size
tblph:		area size
tbldl:		area size
tbldh:		area size
{endasm}