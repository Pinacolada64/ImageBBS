;	print"12/27/90 04:10p"
{asm}
;
;********************************
;* variable handling routines *
;********************************

	;* get descriptor for tt$(.x)
getarr:
	jsr findarr
	ldy #2
	jmp usevar1
	;
	;* get and copy memory to buffer
getln:
	jsr getarr
	ldy var
	sty index
getln0:
	dey
	cpy #$ff
	beq getln1
	cpy #80
	bcs getln0
	lda (var+1),y
	sta buffer,y
	jmp getln0
getln1:
	rts
	;
	;* print tt$(.a)
prtln:
	jsr getarr
	jmp outstr
	;
	;* put descriptor for tt$(.x) and
	;* store buffer in memory
putarr:
	jsr findarr
	ldy #2
	jmp putvar1
	;
	;* put and store string
putln:
	txa
	pha
	lda index
	jsr makeroom
	ldy index
	beq putln2
	dey
putln1:
	lda buffer,y
	sta (var+1),y
	dey
	bpl putln1
putln2:
	pla
	tax
	jmp putarr
	;
	;* find descriptor for tt$(.x)
findarr:
	stx $47
	lda #0
	asl $47
	rol
	sta $48
	txa
	clc
	adc $47
	sta $47
	lda #0
	adc $48
	sta $48
	clc
	lda $2f
	adc $47
	sta $47
	lda $30
	adc $48
	sta $48
	clc
	lda #7
	adc $47
	sta $47
	lda #0
	adc $48
	sta $48
	rts
	;
	;* find basic variable
gvarptr:
	txa
	asl
	tay
	clc
	lda vars,y
	adc $2d
	tax
	lda vars+1,y
	adc $2e
	tay
	rts
varnam:
	jsr gvarptr
	stx $47
	sty $48
	rts
findvar:
	sta $45
	stx $46
	jmp findvar1
	;
	;* print string variable
prtvar:
	jsr usevar
	jmp outstr
	;
	;* print string variable w/mci
prtvar0:
	lda mci
	pha
	lda #0
	sta mci
	jsr prtvar
	pla
	sta mci
	rts
	;
	;* get variable descriptor
usevar:
	jsr varnam
	jmp usevar2
usevar0:
	jsr findvar
usevar2:
	ldy #4
usevar1:
	lda ($47),y
	sta var,y
	dey
	bpl usevar1
	rts
	;
	;* put variable descriptor
putvar:
	jsr varnam
	jmp putvar2
putvar0: jsr findvar
putvar2: ldy #4
putvar1: lda var,y
	sta ($47),y
	dey
	bpl putvar1
	rts
	;
zero: lda #0
	ldy #4
zero1: sta var,y
	dey
	bpl zero1
	rts
minusone:
	jsr zero
	lda #$81
	sta var
	rts
	
	
	;********************************
	;* variables used by ml *
	;********************************
	
	
vars:
	byte "aN" ;0-an$
	byte "a",$80 ;1-a$
	byte "b",$80 ;2-b$
	byte "tR" ;3-tr$
	byte "d",$b1 ;4-d1$
	byte "d",$b2 ;5-d2$
	byte "d",$b3 ;6-d3$
	byte "d",$b4 ;7-d4$
	byte "d",$b5 ;8-d5$
	byte "lD" ;9-ld$
	byte "tT" ;10-tt$
	byte "nA" ;11-na$
	byte "rN" ;12-rn$
	byte "pH" ;13-ph$
	byte "aK" ;14-ak$
	byte "lp" ;15-lp
	byte "pl" ;16-pl
	byte "rc" ;17-rc
	byte "sh" ;18-sh
	byte "mw" ;19-mw
	byte "nl" ;20-nl
	byte "ul" ;21-ul
	byte "qe" ;22-qe
	byte "rq" ;23-rq
	byte "AC" ;24-ac%
	byte "ef" ;25-ef
	byte "lf" ;26-lf
	byte "w",$80 ;27-w$
	byte "p",$80 ;28-p$
	byte "TR" ;29-tr%
	byte "A",$80 ;30-a%
	byte "B",$80 ;31-b%
	byte "DV" ;32-dv%
	byte "dR" ;33-dr$
	byte "c",$b1 ;34-c1$
	byte "c",$b2 ;35-c2$
	byte "cO" ;36-co$
	byte "cH" ;37-ch$
	byte "KP" ;38-kp%
	byte "c",$b3 ;39-c3$
	byte "f",$b1 ;40-f1$
	byte "f",$b2 ;41-f2$
	byte "f",$b3 ;42-f3$
	byte "f",$b4 ;43-f4$
	byte "f",$b5 ;44-f5$
	byte "f",$b6 ;45-f6$
	byte "f",$b7 ;46-f7$
	byte "f",$b8 ;47-f8$
	byte "mP" ;48-mp$
	byte "MN" ;49-mn%
